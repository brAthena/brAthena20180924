//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script:  casamento.txt                         |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/Herc/brA Dev. Team                   |
//   |--------------------------------------------------------|
//   | Versões eA/rA/Herc/brA: 1.1/1.1/1.1/1.0                |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Cabeçalho brAthena [Axl]                           |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

prt_church,97,100,4	script	Casamenteira#w	1_F_LIBRARYGIRL,{
	cutin "wedding_marry01",2;
	if (Upper == 2) {
		mes "[Casamenteira]";
		mes "Olá!";
		mes "Meu nome é Maria e eu";
		mes "estou aqui para munir você";
		mes "com informações relacionadas";
		mes "ao casamento. Agora, você";
		mes "tem alguma pergunta?";
		next;
		if (select("Eu quero me casar.:Eu não preciso de sua ajuda!") == 1) {
			mes "[Casamenteira]";
			mes "Oh, sinto muito, mas";
			mes "crianças não podem";
			mes "se casar.";
			mes "Por que você não desfruta";
			mes "os prazeres simples da";
			mes "infância?";
			goto L_End;
		}
		mes "[Casamenteira]";
		mes "Oh, claro não, você é";
		mes "uma criança pequena";
		mes "não pode se casar-- ";
		mes "há muitas leis contra";
		mes "isso~ Você não é a";
		mes "coisa pequena mais adorável?";
		goto L_End;
	}
	mes "[Casamenteira]";
	mes "Casamento é a bela união";
	mes "de duas almas que tem escolhido";
	mes "estar juntas para sempre,";
	mes "para dividir suas alegrias";
	mes "e vida. Há um alguém especial";
	mes "assim em sua vida?";
	next;
	switch(select("Perguntar sobre a cerimônia de Casamento:Perguntar sobre o Procedimento:Solicitar se casar:Somos do Exército dos Solteiros!")) {
	case 1:
		mes "[Casamenteira]";
		mes "O Sábio e benevolente";
		mes "Rei Tristam III administrava";
		mes "as cerimônias de casamento,";
		mes "mas ele é não tem muito tempo";
		mes "hábil para fazer por causa dos";
		mes "seus deveres reais e horário";
		mes "com seus passos apressados freneticamente.";
		next;
		mes "[Casamenteira]";
		mes "O Bispo Vomars, o bispo do";
		mes "amor, é agora o oficializador";
		mes "das cerimônias de casamento.";
		mes "Ele é verdadeiramente um";
		mes "tesouro para o Reino de Rune-Midgard.";
		next;
		mes "[Casamenteira]";
		mes "Quando você se casa com alguém,";
		mes "é para o resto de sua vida, então";
		mes "pense cuidadosamente antes de fazer";
		mes "ou aceitar uma proposta de casamento.";
		mes "Tenha em mente que um homem pode";
		mes "só casar com uma mulher e vice-versa.";
		next;
		mes "[Casamenteira]";
		mes "Se você está com sorte o";
		mes "bastante para encontrar alguém";
		mes "que você realmente quer para";
		mes "passar o resto de sua vida,";
		mes "pode perguntar ao Papa. Eu";
		mes "espero que todo o mundo encontre seu par perfeito.";
		break;
	case 2:
		mes "[Casamenteira]";
		mes "A primeira parte do procedimento";
		mes "do casamento é canditatar-se";
		mes "por completo. Quando a noiva e";
		mes "o noivo terminarem de se candidatar,";
		mes "eles têm que formar um grupo";
		mes "de dois.";
		next;
		mes "[Casamenteira]";
		mes "Depois de formar um grupo dos dois,";
		mes "o casal deve então falar com o";
		mes "Bispo Vomars. O noivo tem que falar";
		mes "primeiro e deve dizer o nome exato";
		mes "de sua noiva ao Bispo. Caso contrário, a cerimômia parará.";
		next;
		mes "[Casamenteira]";
		mes "Depois, a noiva falará com";
		mes "o Bispo e lhe dirá o nome";
		mes "do noivo dela. Se estes";
		mes "nomes são ditos corretamente";
		mes "ao Bispo, eles poderão permutar";
		mes "os anéis.";
		next;
		mes "[Casamenteira]";
		mes "Quando os anéis de casamento";
		mes "são permutados, o casal está atado no matrimônio. É claro,";
		mes "antes deste ponto, há muitas chances para mudar de ideia, então...";
		next;
		mes "[Casamenteira]";
		mes "Se há muitos casais que";
		mes "querem se casar de uma";
		mes "vez, por favor forme uma";
		mes "fila e falem com o Bispo";
		mes "Vomars na ordem de que só";
		mes "um casal possa se casar de cada vez.";
		next;
		mes "[Casamenteira]";
		mes "Finalmente, fale o nome exato de";
		mes "s"+(Sex?"ua":"eu")+" companheir"+(Sex?"a":"o")+" ao Bispo Vomars,";
		mes "sem desperdiçar muito tempo. Se você levar muito tempo,";
		mes "a cerimônia automaticamente será cancelada e você terá de começar de novo.";
		next;
		mes "[Casamenteira]";
		mes "As noivas precisam se lembrar";
		mes "que eles só têm 3 minutos para";
		mes "terminar de falar com Bispo Vomars";
		mes "depois que o noivo dela terminar";
		mes "de falar com ele.";
		next;
		if (select("Obrigado, isso ajuda muito!:Modo fácil para dizer o nome do meu companheiro?") == 1) {
			mes "[Casamenteira]";
			mes "Bem, eu estou aqui para ajudar";
			mes "os casamentos a proceder tão";
			mes "suavemente quanto possível.";
			mes "Se houver algo que você não";
			mes "entendeu, sinta à vontade";
			mes "para perguntar de novo.";
			goto L_End;
		}
		mes "[Casamenteira]";
		mes "O modo mais fácil para escrever";
		mes "o nome de seu companheiro para o";
		mes "bispo é enviar uma mensagem privada";
		mes "a seu companheiro, e então clique";
		mes "na seção de nome a esquerda que é partida do lembrete de conversa.";
		next;
		mes "[Casamenteira]";
		mes "Aperte o 'Ctrl' e 'C' do";
		mes "teclado para copiar o nome.";
		mes "Então, você pode colar o nome";
		mes "dentro do prompt apertando o";
		mes "'Insert' e 'Shift' do teclado. Parece fácil, certo?";
		next;
		mes "[Casamenteira]";
		mes "Certo, agora deixe-me.";
		mes "Praticar dando o nome";
		mes "de seu companheiro usando";
		mes "o método que eu há pouco descrevi.";
		next;
		input .@partner$;
		mes "[Casamenteira]";
		mes "Certo, depois que você";
		mes "decidir se casar, volte";
		mes "a mim e submeta sua";
		mes "candidatura. Verei você";
		mes "depois aventureiro.";
		break;
	case 3:
		cutin "wedding_marry02",2;
		if (Sex) {
			mes "[Casamenteira]";
			mes "Então você gostaria de se casar?";
			mes "Como um noivo, você precisa preparar";
			mes "^3377FF1 Smoking^000000 e pagar ^3377FF1,300,000 Zeny^000000.";
			mes "Noivas têm que prover os próprios Vestidos de Noiva e pagar uma taxa";
			mes "de 1,200,000 Zeny.";
		}
		else {
			mes "[Casamenteira]";
			mes "Então você gostaria de se casar?";
			mes "Noivas têm que obter os próprios";
			mes "Vestidos de Noiva e pagar uma";
			mes "taxa de 1,200,000 Zeny. Noivos devem";
			mes "trazer um Smoking e pagar 1,300,000 Zeny para se casar.";
		}
		next;
		mes "[Casamenteira]";
		mes "Noivas e Noivos ambos precisam";
		mes "ter ^3377FF1Anel de Diamante^000000 para ser";
		mes "trocado com seu companheiro.";
		mes "Você precisa de todos esses itens";
		mes "prontos quando você submete sua";
		mes "solicitação da cerimônia do casamento.";
		next;
		mes "[Casamenteira]";
		mes "A noiva esperada e o noivo";
		mes "devem ambos ter que completar";
		mes "o processo de solicitação antes";
		mes "que o casamento possa acontecer.";
		mes "Agora, você gostaria de";
		mes "candidatar-se ao matrimônio?";
		next;
		if (select("Sim:Não") == 1) {
			if (getpartnerid()) {
				cutin "wedding_marry02",2;
				mes "[Casamenteira]";
				mes "Eu sinto muito, mas você não";
				mes "pode soilicitar por outro";
				mes "casamento! Eu não posso permitir";
				mes "que traia sua esposa igual a isso,";
				mes "e por outro lado, poligamia não";
				mes "é legal aqui em Rune-Midgard.";
				break;
			}
			else if (wedding_sign == 1) {
				mes "[Casamenteira]";
				mes "Você já não completou a";
				mes "solicitação? Humm, tenha";
				mes "certeza que s"+(Sex?"ua":"eu")+" companheir"+(Sex?"o":"a");
				mes "também terminou o processo";
				mes "de solicitação, e então";
				mes "fale com Bispo Vomars.";
				break;
			}
			else if (BaseLevel < 45) {
				mes "[Casamenteira]";
				mes "Humm, você precisa ser forte";
				mes "o bastante para proteger a";
				mes "pessoa que você ama antes de";
				mes "poder considerar o matrimônio.";
				mes "Depois que você tornar mais";
				mes "forte, vem e fale novamente comigo.";
				goto L_End;
			}
			else if (countitem(2613) < 1) {
				mes "[Casamenteira]";
				mes "Hmm? Você esqueceu de trazer os";
				mes "Anéis de Diamante para trocar";
				mes "com seu companheiro durante a";
				mes "cerimônia de casamento? Procure-o";
				mes "cuidadosamente e volte depois";
				mes "que você achá-lo, certo?";
				goto L_End;
			}
			else if (Sex) {
				if (Zeny < 1300000) {
					mes "[Casamenteira]";
					mes "Sinto muito, mas você não tem";
					mes "os 1,300,000 Zeny";
					mes "que todos os noivos precisam pagar";
					mes "para a cerimônia de casamento.";
					mes "Você esqueceu seu dinheiro?";
					goto L_End;
				}
				else if (countitem(7170) < 1) {
					mes "[Casamenteira]";
					mes "Onde está seu Smoking?";
					mes "Você absolutamente tem que";
					mes "usá-lo durante a cerimônia";
					mes "de casamento! Encontre-o, traga-o";
					mes "para mim, e então nós finalmente poderemos";
					mes "começar o casamento, certo?";
					goto L_End;
				}
			}
			else if (Sex == 0) {
				if (Zeny < 1200000) {
					mes "[Casamenteira]";
					mes "Sinto muito, mas todas as noivas";
					mes "precisam pagar a taxa de 1,200,000";
					mes "Zeny para continuar com a cerimônia de";
					mes "casamento. Talvez";
					mes "você poderia pedir para seu companheiro";
					mes "te ajudar com o dinheiro?";
					goto L_End;
				}
				else if (countitem(2338) < 1) {
					mes "[Casamenteira]";
					mes "Oh querida, você esqueceu";
					mes "seu Vestido de Noiva?";
					mes "Corra e encontre-o, então";
					mes "traga-o para mim você";
					mes "precisa absolutamente";
					mes "dele para o casamento!";
					goto L_End;
				}
			}
			mes "[Casamenteira]";
			mes "Bem, Parace que vocês";
			mes "tem tudo pronto.";
			mes "Embora eu não esteja certa de quem";
			mes "sua companheira seja, deixe-me ser";
			mes "a primeira a dar meus parabéns para vocês";
			mes "em seu casamento a seguir~";
			next;
			mes "[Casamenteira]";
			mes "Agora, vamos começar a";
			mes "solicitação. Por favor escreva";
			mes "abaixo o seu nome correto aqui.";
			next;
			while(1) {
				input .@name$;
				if (.@name$ != strcharinfo(0)) {
					mes "[Casamenteira]";
					mes "Hum, você tem que escrever";
					mes "abaixo o seu nome exatamente";
					mes "como está exibido. Talvez você";
					mes "precise copiar e colá-lo?";
					mes "De qualquer jeito, vamos tentá-lo novamente.";
					next;
				}
				else 
					break;
			}
			mes "[Casamenteira]";
			mes "Ótimo, parece que nós";
			mes "terminamos sua solicitação.";
			mes "Lembre-se que você precisará";
			mes "falar com o Bispo Vomars o nome";
			mes "correto da sua companheira quando";
			mes "você falar com ele mais tarde, certo?";
			next;
			mes "[Casamenteira]";
			mes "Quando sua companheira termina";
			mes "com o processo de solicitação,";
			mes "ambos precisaram";
			mes "falar com o Bispo para começar";
			mes "a cerimônia de casamento.";
			emotion e_lv;
			next;
			mes "[Casamenteira]";
			if (Sex) {
				mes "Desde que você seja o noivo,";
				mes "você precisa falar com";
				mes "o Bispo primeiro. Quando você tiver";
				mes "terminado, será a vez da sua";
				mes "noiva falar com o";
				mes "Bispo Vomars.";
				set Zeny,Zeny-1300000;
				delitem 7170,1; //Tuxedo
			}
			else {
				mes "Desde que você seja a noiva,";
				mes "você precisa esperar que o";
				mes "noivo fale com o Bispo";
				mes "Vomars primeiro. Quando ele tiver";
				mes "terminado, será sua vez de falar";
				mes "o Bispo Vomars.";
				set Zeny,Zeny-1200000;
				delitem 2338,1; //Wedding_Dress
			}
			delitem 2613,1; //Anel de Diamante
			set wedding_sign,1;
			goto L_End;
		}
		mes "[Casamenteira]";
		mes "Não...?";
		mes "Bem, quando você estiver pronto";
		mes "para o matrimônio,";
		mes "sinta-se livre para voltar";
		mes "a mim de forma que você possa aceitar,";
		mes "certo? Tenha um bom dia!";
		break;

	case 4:
		cutin "wedding_marry02",2;
		donpcevent "Exército de Solteiros#Pr::OnEnable";
		donpcevent "Exército de Solteiros#Gf::OnEnable";
		donpcevent "Exército de Solteiros#Mc::OnEnable";
		donpcevent "Exército de Solteiros#Pa::OnEnable";
		donpcevent "Exército de Solteiros#Am::OnEnable";
		donpcevent "Exército de Solteiros#Gn::OnEnable";
		emotion e_omg;
		mes "[Exército de Solteiros]";
		mes "^CC9933Você tem que aprimorar";
		mes "itens por si próprio";
		mes "para fazer um grande equipamento!^000000";
		donpcevent "Exército de Solteiros#Pr::OnEmote";
		next;
		mes "[Exército de Solteiros]";
		mes "^330099É uma perca de tempo";
		mes "formar grupos em";
		mes "cavernas! eu posso";
		mes "fazer isso por si próprio!^000000";
		donpcevent "Exército de Solteiros#Gf::OnEmote";
		next;
		mes "[Exército de Solteiros]";
		mes "^666666Inferno, Eu trenei";
		mes "tudo por mim mesmo desde que nasci,";
		mes "e de todo modo";
		mes "para mudar meu trabalho!^000000";
		donpcevent "Exército de Solteiros#Mc::OnEmote";
		next;
		mes "[Exército de Solteiros]";
		mes "^666600Eu ESCOLHI passar";
		mes "o Natal sozinho...";
		mes "jogando solitariamente e";
		mes "fazendo encruzilhadas de palavras!^000000";
		donpcevent "Exército de Solteiros#Pa::OnEmote";
		next;
		mes "[Exército de Solteiros]";
		mes "^CC9966Mulheres podem quebrar meu";
		mes "espírito, mas elas nunca";
		mes "lavaram... minha LIBERDADE!^000000";
		donpcevent "Exército de Solteiros#Am::OnEmote";
		next;
		mes "[Exército de Solteiros]";
		mes "^669900...Nós somos livres! nós somos";
		mes "o Invencível Exército de Solteiros!^000000";
		donpcevent "Exército de Solteiros#Gn::OnEmote";
		close2;
		cutin "wedding_marry01",255;
		emotion e_swt;
		donpcevent "Exército de Solteiros#Pr::OnInit";
		donpcevent "Exército de Solteiros#Gf::OnInit";
		donpcevent "Exército de Solteiros#Mc::OnInit";
		donpcevent "Exército de Solteiros#Pa::OnInit";
		donpcevent "Exército de Solteiros#Am::OnInit";
		donpcevent "Exército de Solteiros#Gn::OnInit";
		end;
	}
	close2;
	cutin "wedding_marry01",255;
	end;

L_End:
	close2;
	cutin "",255;
	end;
}

prt_church,97,102,0	script	Exército de Solteiros#Pr	8W_SOLDIER,{
	mes "[Exército de Solteiros]";
	mes "^CC9933Você tem que aprimorar";
	mes "itens por si próprio";
	mes "para fazer um grande equipamento!^000000";
	close;

OnInit:
	disablenpc "Exército de Solteiros#Pr";
	end;

OnEnable:
	enablenpc "Exército de Solteiros#Pr";
	emotion e_go;
	end;

OnEmote:
	emotion e_rock;
	end;
}

prt_church,98,102,0	script	Exército de Solteiros#Gf	4_M_GEF_SOLDIER,{
	mes "[Exército de Solteiros]";
	mes "^330099É uma perca de tempo";
	mes "formar grupos em";
	mes "cavernas! eu posso";
	mes "fazer isso por si próprio!^000000";
	close;

OnInit:
	disablenpc "Exército de Solteiros#Gf";
	end;

OnEnable:
	enablenpc "Exército de Solteiros#Gf";
	emotion e_go;
	end;

OnEmote:
	emotion e_rock;
	end;
}

prt_church,99,102,0	script	Exército de Solteiros#Mc	4_M_MOC_SOLDIER,{
	mes "[Exército de Solteiros]";
	mes "^666666Inferno, Eu trenei";
	mes "tudo por mim mesmo desde que";
	mes "nasci, e de todo modo";
	mes "para mudar meu trabalho!^000000";
	close;

OnInit:
	disablenpc "Exército de Solteiros#Mc";
	end;

OnEnable:
	enablenpc "Exército de Solteiros#Mc";
	emotion e_go;
	end;

OnEmote:
	emotion e_rock;
	end;
}

prt_church,100,102,0	script	Exército de Solteiros#Pa	4_M_PAY_SOLDIER,{
	mes "[Exército de Solteiros]";
	mes "^666600Eu ESCOLHI passar";
	mes "o Natal sozinho...";
	mes "jogando solitariamente e";
	mes "fazendo encruzilhadas de palavras!^000000";
	close;

OnInit:
	disablenpc "Exército de Solteiros#Pa";
	end;

OnEnable:
	enablenpc "Exército de Solteiros#Pa";
	emotion e_go;
	end;

OnEmote:
	emotion e_rock;
	end;
}

prt_church,101,102,0	script	Exército de Solteiros#Am	8_M_JPNSOLDIER,{
	mes "[Exército de Solteiros]";
	mes "^CC9966Mulheres podem quebrar meu";
	mes "espírito, mas elas nunca";
	mes "levaram... minha LIBERDADE!^000000";
	close;

OnInit:
	disablenpc "Exército de Solteiros#Am";
	end;

OnEnable:
	enablenpc "Exército de Solteiros#Am";
	emotion e_go;
	end;

OnEmote:
	emotion e_rock;
	end;
}

prt_church,102,102,0	script	Exército de Solteiros#Gn	8_M_TWSOLDIER,{
	mes "[Exército de Solteiros]";
	mes "^669900...Nós somos livres! nós somos";
	mes "o Invencível Exército de Solteiros!^000000";
	close;

OnInit:
	disablenpc "Exército de Solteiros#Gn";
	end;

OnEnable:
	enablenpc "Exército de Solteiros#Gn";
	emotion e_go;
	end;

OnEmote:
	emotion e_rock;
	end;
}

prt_church,100,128,4	script	Bispo#w	1_M_PASTOR,{
	cutin "wedding_bomars01",2;
	if (Upper == 2) {
		mes "[Vomars]";
		mes "Saudações, criança.";
		mes "você está perdido? Hum.";
		mes "Você sabe onde sua";
		mes "mamãe e seu papai estão?";
		close2;
		cutin "wedding_bomars01",255;
		end;
	}

	if (!getpartnerid()) {
		if (!$wedding) {
			if (wedding_sign == 1) {
				getpartymember(getcharid(1));
				set .@partymembercount,$@partymembercount;
				if (.@partymembercount == 2) {
					if (Sex) {
						set $wedding,1;
						initnpctimer;
						mes "[Vomars]";
						mes "Jovens Apaixonados, por favor";
						mes "lembrem-se desde momento pelo";
						mes "resto de suas vidas. Que o futuro de vocês seja abençoado com paz";
						mes "e alegria. Que o amor de vocês possa";
						mes "crescer mais a cada dia que passa.";
						next;
						mapannounce "prt_church","É o pedido de matrimônio do noivo, Senhor " + strcharinfo(0) + "...",bc_map;
						mes "[Vomars]";
						mes "Até o fim do mundo,";
						mes "você deve ficar ao lado";
						mes "daquela que você";
						mes "realmente ama, para";
						mes "apoiá-la e protegê-la. Agora, deixe";
						mes "eu saber o nome da sua noiva?";
						next;
						input $wed_bride$;
						mes "[Vomars]";
						mes "Senhor " + strcharinfo(0) + "...";
						mes "Você jura pela sua vida";
						mes "que para sempre você vai amá-la";
						mes "e tomará conta da sua noiva,";
						mes "Senhorita " + $wed_bride$ + "?";
						next;
						select("Eu aceito.");
						set $wed_groom$,strcharinfo(0);
						mes "[Vomars]";
						mes "Agora, é a vez da sua";
						mes "noiva fazer";
						mes "seu voto de casamento.";
						mes "Que ela venha aqui na frente...";
						close2;
						mapannounce "prt_church","O noivo, Senhor " + strcharinfo(0) + ", fez seus votos para a Senhorita " + $wed_bride$ + "...",bc_map;
						cutin "",255;
						end;
					}
					mes "[Vomars]";
					mes "Sinto muito, mas o noivo";
					mes "precisa falar comigo primeiro nessa";
					mes "ordem para começar o casamento.";
					mes "É um antigo protocolo formado,";
					mes "mas eu admito que ele mantém";
					mes "que as coisas corram suavemente.";
					goto L_End;
				}
				mes "[Vomars]";
				mes "Antes que você possa";
				mes "estar casado, primeiro você precisar";
				mes "fazer um grupo de";
				mes "de dois com sua companheira.";
				mes "Assim, nós poderemos prosseguir";
				mes "com a cerimônia.";
				goto L_End;
			}
			mes "[Vomars]";
			mes "Vocês precisam acertar";
			mes "o matrimônio com a casamenteira";
			mes "antes que vocês consigam casar.";
			mes "A Casamenteira dirá";
			mes "o que vocês precisam fazer";
			mes "para preparar o matrimônio.";
			goto L_End;
		}
		else if ($wedding == 1) {
			if (wedding_sign == 1) {
				getpartymember(getcharid(1));
				set .@partymembercount,$@partymembercount;
				if (.@partymembercount == 2) {
					if (Sex == 0) {
						if (strcharinfo(0) == $wed_bride$) {
							mes "[Vomars]";
							mes "Jovens Apaixonados, por favor";
						        mes "lembrem se desde momento pelo";
						        mes "resto das suas vidas. Que o futuro de vocês seja abençoado com paz";
						        mes "e alegria. Que o amor de vocês possa";
						        mes "crescer mais a cada dia que passa.";
							next;
							mapannounce "prt_church","Vamos ouvir o que a noiva, Senhorita "+$wed_bride$+", tem a dizer...",bc_map;
							mes "[Vomars]";
							mes "Senhorita "+$wed_bride$+"...";
							mes "Você jura ficar";
							mes "verdadeiramente com "+$wed_groom$+",";
							mes "estar ao seu lado, não importa";
							mes "o quanto perigoso possa ser?";
							next;
							if (select("^FF0000Não.^000000:Eu aceito.") == 1) {
								cutin "wedding_bomars03",2;
								mapannounce "prt_church","Próximo casal, por favor prossiga...",bc_map;
								mes "[Vomars]";
								mes "Então "+$wed_groom$;
								mes "não é o que";
								mes "queria casar? Hum.";
								mes "Eu sinto profudamente por";
								mes "este mal entendido...";
								set $wedding,0;
								close2;
								stopnpctimer;
								cutin "",255;
								end;
							}
							mes "[Vomars]";
							mes "Você jura verdadeiramente";
							mes "fidelidade e paciência?";
							mes "você casará com "+$wed_groom$+"?";
							next;
							if (select("Sim, Eu caso.:^FF0000Não.^000000") == 1) {
								if (isloggedin(getcharid(3,$wed_groom$))) {
									if (marriage($wed_groom$)) {
										//Call Wedding effect
										wedding;
										//Dá o anel para a Noiva, e muda o espirito de casamento.
										sc_start SC_WEDDING,3600000,1;
										getitem 2635,1; //Bride_Ring
										//Dá o anel ao Noivo, e muda o espirito de casamento.
										attachrid(getcharid(3,$wed_groom$));
										sc_start SC_WEDDING,3600000,1;
										getitem 2634,1; //Bridegroom_Ring
										detachrid;
										//Switch Script progression back to Bride
										attachrid(getcharid(3,$wed_bride$));
										cutin "wedding_bomars02",2;
										mapannounce "prt_church","Agora Eu os declaro, "+$wed_groom$+" e "+$wed_bride$+", Marido e Mulher.",bc_map;
										mes "[Vomars]";
										mes "Pelo poderes a mim concebidos";
										mes "como Bispo Real do Reino";
										mes "de Rune-Midgard,";
										mes "agora eu os declaro";
										mes "marido e mulher. Que o futuro de vocês seja";
										mes "abençoado com muitas alegrias.";
										next;
										mes "[Vomars]";
										mes "E por último...";
										mes "Sempre sejam felizes,";
										mes "querida "+$wed_bride$+"...";
										set $wed_groom$,"";
										set $wed_bride$,"";
										set $wedding,0;
										close2;
										stopnpctimer;
										cutin "",255;
										detachrid;
										end;
									}
								}
								cutin "wedding_bomars03",2;
								mes "[Vomars]";
								mes "Hum. Parece que seu";
								mes "noivo saiu antes da";
								mes "cerimônia termina.";
								mes "Por favor tente novamente assim que ele";
								mes "retornar.";
								goto L_End;
							}
							else {
								cutin "wedding_bomars03",2;
								mapannounce "prt_church","Deus! "+$wed_bride$+" Rejeitou o pedido de casamento de "+$wed_groom$+"!",bc_map;
								mes "[Vomars]";
								mes "Hum. Parece que você";
								mes "mudou de ideia.";
								mes "Embora eu sinto muito";
								mes "pelo o noivo, você";
								mes "deve fazer o que o seu coração";
								mes "falar que é correto. Agora, corra!";
							}
							set $wed_groom$,"";
							set $wed_bride$,"";
							set $wedding,0;
							close2;
							stopnpctimer;
							cutin "",255;
							end;
						}
						callsub S_Busy;
					}
					callsub S_Busy;
				}
				callsub S_Busy;
			}
			if (strcharinfo(0) == $wed_bride$) {
				mes "[Vomars]";
				mes "Hum? Parece que a";
				mes "Casamenteira ainda não";
				mes "recebeu sua solitação de";
				mes "matrimônio. Por favor fale";
				mes "com ela assim nós poderemos";
				mes "começar a cerimônia de casamento.";
				goto L_End;
			}
			callsub S_Busy;
		}
		callsub S_Busy;
	}
	mes "[Vomars]";
	mes "Eu desejo a vocês";
	mes "felicidades eternas. Não importa";
	mes "como escuro o presente possa ser,";
	mes "sempre fique ao lado";
	mes "de sua amada e olhe";
	mes "para o futuro com esperança.";

L_End:
	close2;
	cutin  "",255;
	end;

S_Busy:
	if ($wed_groom$ != "" && $wed_bride$ != "") {
		mes "[Vomars]";
		mes "O casamento da";
		mes "Senhorita "+ $wed_bride$ +" e do";
		mes "Senhor "+ $wed_groom$;
		mes "está em andamento.";
		mes "Por favor controle sua voz.";
		goto L_End;
	}
	mes "[Vomars]";
	mes "^FFFFFFEstou conduzindo um casamento";
	mes "para outro casal agora, então";
	mes "por favor espere pacientimente por sua";
	mes "vez. Obrigado pela compreensão...^000000";
	goto L_End;

OnInit:
	set $wedding,0;
	end;

OnStop:
	stopnpctimer;
	end;

OnReset:
	set $wed_groom$,"";
	set $wed_bride$,"";
	set $wedding,0;
	end;

OnTimer180000:
	mapannounce "prt_church","Você respondeu tarde demais... Próximo casal, por favor prossiga.",bc_map;
	set $wed_groom$,"";
	set $wed_bride$,"";
	set $wedding,0;
	stopnpctimer;
	end;
}