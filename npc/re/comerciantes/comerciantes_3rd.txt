//===== Hercules Script ======================================
//= 3rd Item Seller
//===== By: ==================================================
//= Masao, Mercurial
//===== Current Version: =====================================
//= 1.5
//===== Description: =========================================
//= [Official Conversion]
//= Sells some 3rd Job related items:
//= - Poison Herbs, Rune Stones, Rare Herbs, Points
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Optimized Poison Herb Salesman NPC
//= 1.2 Optimized and standardized. [Euphy]
//= 1.3 Added Malangdo/Mora Points NPC. [Euphy]
//= 1.4 Updated to match the official scripts. [Euphy]
//= 1.5 Added Izlude duplicates. [Euphy]
//============================================================

// Negociante de Ervas (Sicário)
//============================================================
-	script	::phs	-1,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "- Espere um minuto !! -";
		mes "- Atualmente você está carregando -";
		mes "- muitos itens com você. -";
		mes "- Por favor, tente novamente -";
		mes "- depois de perder um pouco desse peso. -";
		close;
	}
	mes "[Negociante de Ervas]";
	mes "Eu sou uma profissional em";
	mes "Vender Ervas Venenosas.";
	mes "Cada erva custa 4,000 zenys.";
	mes "Entao? O que você gostaria de comprar?";
	next;
	.@i = select("Nerium:Lantana:Makulrata:Celatom:Scoforia:Amoena:Kit de Veneno:Cancelar")-1;
	mes "[Negociante de Ervas]";
	if (.@i < 6) {
		.@item = 7932+.@i;
		.@price = 4000;
		.@max = 2000;
		.@max$ = "2,000";
	} else if (.@i == 6) {
		.@item = 7931;
		.@price = 5000;
		.@max = 500;
		.@max$ = "500";
		mes "Cada Kit de Veneno custa 5,000 zenys.";
	} else {
		mes "Bem, Eu entendo. Volte Sempre~!";
		close;
	}
	mes "Então quantas você irá querer?";
	mes "Você pode comprar no máximo '"+.@max+"' de cada.";
	mes "Digite '0' caso queira cancelar a negociação.";
	next;
	input .@amount;
	if (.@amount == 0) {
		mes "[Negociante de Ervas]";
		mes "Você cancelou a negociação.";
		close;
	}
	if (.@amount > .@max) {
		mes "[Negociante de Ervas]";
		mes "O número deve ser inferior a "+.@max$+"!";
		close;
	}
	.@total = .@amount * .@price;
	if (Zeny < .@total) {
		mes "[Negociante de Ervas]";
		mes "Parece que você não tem dinheiro suficiente.";
		mes "Verifique o preço do que você quer comprar novamente.";
		close;
	}
	Zeny -= .@total;
	getitem .@item, .@amount;
	mes "[Negociante de Ervas]";
	mes "Muito Obrigada. Volte Sempre~!";
	close;
}
job3_guil01,79,96,3	duplicate(phs)	Negociante de Ervas	4_F_YUNYANG
morocc,191,94,4	duplicate(phs)	Negociante de Ervas#moc	4_F_YUNYANG	//Oficial: (190,96) -> na parede
lhz_in02,16,205,4	duplicate(phs)	Negociante de Ervas#lhz	4_F_YUNYANG

// Negociante de Runas (Cavaleiro Rúnico)
//============================================================
-	script	::runesale	-1,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "- Espere um Minuto !! -";
		mes "- Atualmente você está carregando -";
		mes "- muitos itens com você. -";
		mes "- Por favor, volte mais tarde -";
		mes "- depois de perder um pouco desse peso. -";
		close;
	}
	mes "[Negociante de Runas]";
	mes "Ei, você precisa de Runas?!";
	mes "Eu vendo todos os tipos de Pedras Rúnicas!";
	mes "Qual você gostaria de comprar?";
	next;
	.@i = select("Comprar Runa Bruta Forte.:Comprar Runa Bruta Comum.");
	mes "[Negociante de Runas]";
	if (.@i == 1) {
		mes "Runa Bruta Forte!";
		mes "Irá custar 2,500 zeny cada!";
		.@item = 12734; //Runa_Bruta_Forte
		.@price = 2500;
	} else {
		mes "Runa Bruta Comum?";
		mes "Irá custar 1,000 zeny cada!";
		.@item = 12737; //Runa_Bruta_Comum
		.@price = 1000;
		setarray .@rune[0],12737,1000;
	}
	mes "Diga-me quantas você quer,";
	mes "e lembre-se, você só pode";
	mes "comprar um máximo de '2000'.";
	next;
	input .@amount;
	if (.@amount == 0) {
		mes "[Negociante de Runas]";
		mes "Você não está comprando? Por favor vá embora!";
		close;
	}
	if (.@amount > 2000) {
		mes "[Negociante de Runas]";
		mes "A quantidade máxima é de 2,000!";
		close;
	}
	.@total = .@amount * .@price;
	if (Zeny < .@total) {
		mes "[Negociante de Runas]";
		mes "Zenys! Zenys!";
		mes "Você não tem dinheiro suficiente..";
		mes "Confira seu dinheiro primeiro.";
		close;
	}
	if (!checkweight(.@item,.@amount)) {
		mes "[Negociante de Runas]";
		mes "Parece que você não é capaz de carregar tudo isso não é mesmo?!";
		close;
	}
	Zeny -= .@total;
	getitem .@item, .@amount;
	mes "[Negociante de Runas]";
	mes "Obrigado.";
	mes "Por favor, volte sempre~!";
	close;
}
job3_rune01,90,62,3	duplicate(runesale)	Negociante de Runas#job3	4_M_YURI
prontera,168,228,3	duplicate(runesale)	Negociante de Runas#prt	4_M_YURI

// Negociante de Ervas Raras (Sicário)
//============================================================
job3_guil01,91,93,3	script	Negociante de Ervas Raras	1_M_03,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "- Espere um minuto! -";
		mes "- Atualmente você está carregando -";
		mes "- muitos itens com você. -";
		mes "- Por favor, volte novamente -";
		mes "- apos deixar alguns itens no Armazém. -";
		close;
	}
	if (Class == Job_Guillotine_Cross || Class == Job_Guillotine_Cross_T || Class == Job_Baby_Cross) {
		mes "[Negociante de Ervas Raras]";
		mes "Eu ando ao redor do mundo e recolho ervas venenosas raras. Recentemente, comecei a lidar com uma erva chamada Izidor. Se você estiver interessado, você pode comprá-la.";
		next;
		.@i = select("Como posso comprá-la?:Trocá-la por um Sangue de Animal:Trocá-la por uma Erva Amarga:Trocá-la por uma Erva Letal:Trocá-la por uma Rosa de Gelo:Trocá-la por uma Menta:Trocá-la por uma Hinalle")-2;
		if (.@i == -1) {
			mes "[Negociante de Ervas Raras]";
			mes "Você pode comprá-la por 10,000 zeny e um desses itens raros: Sangue de Animal, Erva Amarga, Erva Letal, Rosa de Gelo, Menta ou Hinalle.";
			next;
			mes "[Negociante de Ervas Raras]";
			mes "Por que eu peço dinheiro? Isso é apenas para pagar um pouco de respeito para um colecionador como eu. Ha ha ha...";
			close;
		} else {
			setarray .@exchange[0],702,621,631,749,605,703;
			.@item = .@exchange[.@i];
			.@price = 10000;

			mes "[Negociante de Ervas Raras]";
			mes "Quantas você precisa?";
			mes "Você pode comprar no máximo '2000'.";
			mes "Digite '0' se desejar cancelar.";
			next;
			input .@amount;
			if (.@amount == 0) {
				mes "[Negociante de Ervas Raras]";
				mes "Você cancelou a negociação.";
				close;
			}
			if (.@amount > 2000) {
				mes "[Negociante de Ervas Raras]";
				mes "O máximo que posso negociar é 2,000!";
				close;
			}
			.@total = .@amount * .@price;
			if (countitem(.@item) < .@amount || Zeny < .@total) {
				mes "[Negociante de Ervas Raras]";
				mes "Opa, acho que você não tem todos os requerimentos necessários para a negociação.";
				close;
			}
			mes "[Negociante de Ervas Raras]";
			mes "Excelente. Recebi o dinheiro e o item especial.";
			delitem .@item, .@amount;
			Zeny -= .@total;
			getitem 709, .@amount; //Izidor
			close;
		}
	}
	mes "[Negociante de Ervas Raras]";
	mes "Eu ando ao redor do mundo e recolho ervas venenosas raras. Mas eu não posso vender minhas ervas para uma pessoa como você... Ha ha ha...";
	close;
}

// Negociante de Fragmentos (Feiticeiro)
//============================================================
-	script	::pss	-1,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "- Espere um minuto! -";
		mes "- Atualmente você está carregando -";
		mes "- muitos itens com você. -";
		mes "- Por favor, volte novamente -";
		mes "- apos deixar alguns itens no Armazém. -";
		close;
	}
	mes "[Negociante de Fragmentos]";
	mes "Olá. Estou vendendo um catalizador chamado de ^FF0000Fragento^000000 para Feiticeiros. Qual desses você gostaria de comprar?";
	next;
	.@i = select("Fragmento Escarlate - 200z:Fragmento Verde - 200z:Fragmento Azul - 200z:Fragmento Amarelo - 200z:Cancelar")-1;
	mes "[Negociante de Fragmentos]";
	if (.@i == 4) {
		mes "Você não encontrou o que precisava?";
		close;
	}
	setarray .@itemid[0],6360,6363,6361,6362;
	setarray .@color$[0],"Vermelho","Verde","Azul","Amarelo";
	mes "Você escolheu o Fragmento "+.@color$[.@i]+".";
	mes "De quantos você precisa?";
	mes "Se desejar cancelar, digite '0'.";
	next;
	input .@amount;
	mes "[Negociante de Fragmentos]";
	if (.@amount == 0) {
		mes "Você cancelou a negociação.";
		close;
	}
	if (Zeny < .@amount*200) {
		mes "Você não tem dinheiro suficiente.";
		close;
	}
	if (!checkweight(.@itemid[.@i],.@amount)) {
		mes "Você não tem espaço suficiente no seu inventário para comprar essa quantidade.";
		close;
	}
	Zeny -= (.@amount*200);
	getitem .@itemid[.@i],.@amount;
	mes "Muito orbigada. Te vejo por aí~!";
	close;
}
gef_tower,105,172,5	duplicate(pss)	Negociante de Fragmentos#Sorcerer	8_F_GIRL
prt_in,131,66,0	duplicate(pss)	Negociante de Fragmentos#Prontera	8_F_GIRL
alberta,105,52,7	duplicate(pss)	Negociante de Fragmentos#Alberta	8_F_GIRL
aldebaran,133,114,5	duplicate(pss)	Negociante de Fragmentos#Aldebaran	8_F_GIRL
comodo,193,159,5	duplicate(pss)	Negociante de Fragmentos#Comodo	8_F_GIRL
geffen,129,49,5	duplicate(pss)	Negociante de Fragmentos#Geffen	8_F_GIRL
izlude,138,163,5	duplicate(pss)	Negociante de Fragmentos#Izlude	8_F_GIRL	// Coordenadas antiga: izlude (135,121)
izlude_a,138,163,5	duplicate(pss)	Negociante de Fragmentos#Izlude_a	8_F_GIRL
izlude_b,138,163,5	duplicate(pss)	Negociante de Fragmentos#Izlude_b	8_F_GIRL
izlude_c,138,163,5	duplicate(pss)	Negociante de Fragmentos#Izlude_c	8_F_GIRL
izlude_d,138,163,5	duplicate(pss)	Negociante de Fragmentos#Izlude_d	8_F_GIRL
malangdo,214,163,5	duplicate(pss)	Negociante de Fragmentos#Malangdo	8_F_GIRL
mora,115,118,3	duplicate(pss)	Negociante de Fragmentos#Mora	8_F_GIRL
ra_in01,256,273,3	duplicate(pss)	Negociante de Fragmentos#Rachel	8_F_GIRL
veins,202,128,5	duplicate(pss)	Negociante de Fragmentos#Veins	8_F_GIRL
dicastes01,207,200,5	duplicate(pss)	Negociante de Fragmentos#Dicastes	8_F_GIRL
manuk,261,206,3	duplicate(pss)	Negociante de Fragmentos#Manuk	8_F_GIRL
splendide,207,160,5	duplicate(pss)	Negociante de Fragmentos#Splendide	8_F_GIRL
mid_camp,224,237,3	duplicate(pss)	Negociante de Fragmentos#Midgard	8_F_GIRL