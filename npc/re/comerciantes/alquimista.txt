//===== Hercules Script ======================================
//= Alchemist Shop
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.0
//===== Description: =========================================
//= [Official Conversion]
//= Merchants for Alchemist Materials and Manuals.
//===== Additional Comments: =================================
//= 1.0 Split Geneticist shop from main file. [Euphy]
//=     Updated dialogue to match the official script.
//============================================================

alde_alche,31,186,3	script	Negociante de Livros#alde	4_M_ALCHE_D,{
	mes "[Negociante de Livros]";
	mes "Seja bem vindo.";
	mes "Estou aqui para vender";
	mes "^0000FFLivros Especiais^000000 para Bioquímicos.";
	next;
	mes "[Negociante de Livros]";
	mes "Bioquímicos possuem muitas habilidades";
	mes "que necessitam de livros para serem ativadas.";
	next;
	mes "[Negociante de Livros]";
	mes "Quanto mais livros você tem,";
	mes "Mais habilidades você poderá usar.";
	next;
	mes "[Negociante de Livros]";
	mes "Por favor tenha em mente que";
	mes "Eu sou Especialista na venda destes livros e";
	mes "Eu ^FF0000não vendo outros itens^000000.";
	next;
	mes "[Negociante de Livros]";
	mes "Gostaria de dar uma olhada em meus livros?";
	next;
	if(select("Certo.:Não, Obrigado.") == 2) {
		mes "[Negociante de Livros]";
		mes "Ok. Obrigado por visitar minha loja.";
		mes "Passar bem.";
		close;
	}
	if (checkweight(1201,1) == 0) {
		mes "[Negociante de Livros]";
		mes "Sinto muito, mas o seu inventório está lotado de coisas.";
		mes "Esvazie ele primeiro.";
		close;
	}
	if (MaxWeight - Weight < 2500) {
		mes "[Negociante de Livros]";
		mes "Por favor, esvazie o seu inventório primeiro.";
		close;
	}
	mes "[Negociante de Livros]";
	mes "Ok, então, aqui está a lista de livros disponíveis para compra.";
	next;
	switch(select("[Livro de Bombas de Maçã] 100,000 zenys:[Livro de Bombas de Abacaxi] 100,000 zenys:[Livro de Bombas de Coco] 100,000 zenys:[Livro de Bombas de Melão] 100,000 zenys:[Livro de Bombas de Banana] 100,000 zeny:[Livro de Genética Vegetal] 100,000 zenys:[Livro de Farmacologia] 100,000 zenys:[Livro de Culinária Avançada] 100,000 zenys:[Livro de Aumento de Força] 100,000 zenys:[Livro de Aumento de Energia] 100,000 zenys:Fechar")){
	case 1: .@item = 6279; break;
	case 2: .@item = 6280; break;
	case 3: .@item = 6281; break;
	case 4: .@item = 6282; break;
	case 5: .@item = 6283; break;
	case 6: .@item = 6284; break;
	case 7: .@item = 6285; break;
	case 8: .@item = 11022; break;
	case 9: .@item = 11023; break;
	case 10: .@item = 11024; break;
	case 11:
		mes "[Negociante de Livros]";
		mes "Obrigado pelo seu patrocínio.";
		mes "Por favor venha novamente qualquer hora dessas.";
		close;
	}
	mes "[Negociante de Livros]";
	mes "^0000FF["+getitemname(.@item)+"]^000000 custa";
	mes "100,000 zenys.";
	mes "Quantos você gostaria de comprar?";
	next;
	input .@amount;
	if (.@amount == 0) {
		mes "[Negociante de Livros]";
		mes "Gostaria de ver alguns livros diferentes?";
		close;
	}
	if (.@amount < 1 || .@amount > 99 ){
		mes "[Negociante de Livros]";
		mes "Você não pode comprar mais de 100 por vez.";
		close;
	}
	mes "[Negociante de Livros]";
	mes "Você escolheu "+.@amount+"x ^0000FF["+getitemname(.@item)+"]^000000 para serem comprados.";
	mes "Gostaria de continuar?";
	next;
	if (select("Sim:Não") == 1) {
		.@total = .@amount * 100000;
		if (Zeny < .@total) {
			mes "[Negociante de Livros]";
			mes "Desculpe, mas você não tem dinheiro suficiente.";
			close;
		}
		if (checkweight(.@item,.@amount) == 0){
			mes "[Negociante de Livros]";
			mes "Não parece que você pode carregar tudo isso.";
			mes "Por favor, cheque o espaço em seu inventório.";
			close;
		}
		mes "[Negociante de Livros]";
		mes "Obrigado pelo seu patrocínio.";
		Zeny -= .@total;
		getitem .@item,.@amount;
		close;
	}
	mes "[Negociante de Livros]";
	mes "Por favor, escolha sábiamente";
	mes "antes de tomar sua decisão.";
	close;
}