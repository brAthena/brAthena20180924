//===== Hercules Script ======================================
//= Negociante de Diamantes
//===== By: ==================================================
//= Z3R0
//===== Current Version: =====================================
//= 1.5
//===== Description: =========================================
//= [Official Conversion]
//= Exchanges 17 Carat Diamond
//===== Additional Comments: =================================
//= 1.0 First / Optimized Version
//= 1.1 Adjusted for Missing ; on Line #271 (#15425)
//= 1.2 Removed Comments per Request (#15426)
//= 1.3 Changed Emotion # to Const Definition (#15427)
//= 1.4 Removed Double Space on Line #148 (#15428)
//= 1.5 Added This Version History (#15429)
//============================================================

-	script	NegociantedeDiamantes	1_M_MERCHANT,{

	.@npc$ = "[Negociante de Diamantes]";

	mes .@npc$;
	mes "Senhoras e Senhores! ! !";
	mes "Vocês já devem ter ouvido rumores, mas nunca viram com seus próprios olhos";
	mes "O diamante mais raro do mundo!";
	mes "O ^FF82FF'Diamante de 17 Quilates'^000000 Está atualmente sendo vendido!!";
	mes "Se diamantes valiosos interessa a você, ouça com atenção!";
	next;
	
	mes .@npc$;
	mes "Você pode da-lo de presente para o seu amor.";
	mes "A pessoa que receber o ^FF82FF'Diamante de 17 Quilates'^000000 certamente irá apreciar";
	mes "este tesouro com todo o seu coração.";
	emotion e_loud;
	next;
	
	mes .@npc$;
	mes "O que? Você não tem um amor?";
	mes "Ainda sim, você pode apreciá-lo por conta própria.";
	mes "A aura encantadora do ^FF82FF'Diamante de 17 Quilates'^000000 é maravilhosa";
	mes "para trazer a pessoa amada a sua vida.";
	next;
	
	mes .@npc$;
	mes "Conhecido como o diamante dos diamantes...";
	mes "Esté é o ^FF82FF'Diamante de 17 Quilates'^000000!";
	next;

	.@menu$ = "^FF82FF'Diamante de 17 Quilates'^000000?";
	if (countitem(6024))
		.@menu$ += ":Trocar Diamante de 17 Quilates por Zenys...";

	switch(select(.@menu$)) {
	case 1:
		mes .@npc$;
		mes "Sim!";
		mes "Hehe, Aqui está veja o ^FF82FF'Diamante de 17 Quilates'^000000.";
		mes "É um dos diamantes de maior qualidade do mundo.";
		mes "O melhor dos melhores... Comparado com qualquer mini-rocha incolor que você pode achar";
		mes "até no de Morroc!";
		emotion e_omg;
		next;
		
		mes .@npc$;
		mes "Alguns afirmam que este é o mesmo diamante";
		mes "que foi usado por Kachua em Comodo!";
		next;
		
		if (select("Encerrar Conversa:Quanto ele custa?") == 1) {
			mes .@npc$;
			mes "É bastante barato!";
			mes "Se você estiver interessado, fale comigo a qualquer momento!";
			close;
		}
		
		mes .@npc$;
		mes "Claro! A coisa mais importante é o preço!";
		mes "Como eu disse anteriormente, o ^FF82FF'Diamante de 17 Quilates'^000000 foi";
		mes "avaliado como uma super rocha rara.";
		mes "Não há muitos como ela no mundo.";
		next;
		
		mes .@npc$;
		mes "O preço é de apenas 500 milhões de zeny!";
		mes "Há também uma taxa de serviço de 1.000.000z.";
		mes "Em comparação com a qualidade do diamante";
		mes "este é um pequeno preço a se pagar.";
		next;
		
		if (select("^828282É muito caro.^000000:Vou comprá-lo.") == 1) {
			mes .@npc$;
			mes "Dizer que um diamante como o";
			mes "^FF82FF'Diamante de 17 Quilates'^000000 é muito caro, ";
			mes "Você me deixa sem palavras.";
			close;
		}
		
		mes .@npc$;
		mes "OOOH! Eu sabia desde o momento em que te vi que você era o legítimo";
		mes "proprietário deste maravilhoso diamante.";
		mes "Estou feliz por não estar errado!";
		mes "Eu acredito que este diamante irá cair muito bem em você.";
		mes "Tudo que eu preciso de é concluir este contrato.";
		next;
		
		while(1) {
			if (select("^B9062FOk, assinarei o contrato.^000000:^828282Deixa pra lá, mudei de idéia.^000000") == 2) {
				mes .@npc$;
				mes "Entendo, isso é muito ruim...";
				close;
			}
			
			mes .@npc$;
			mes "Como você se chama?";
			next;
			
			mes "[" + strcharinfo(0) + "]";
			mes "Eu me chamo " + strcharinfo(0) + ".";
			next;

			mes .@npc$;
			mes "Tudo bem, assine aqui por favor.";
			next;
			
			input .@charname$;
			if (.@charname$ == strcharinfo(0)) {
				mes .@npc$;
				mes "Ok, bom.";
				mes "Tudo o que resta é pagamento.";
				next;
				
				if (Zeny >= 501000000) {
					mes .@npc$;
					mes ". . . . . .";
					next;
					
					mes .@npc$;
					mes "Cálculando o preço junto a taxa de serviço..";
					mes "501,000,000 zenys.";
					mes "O valor foi confirmado.";
					emotion e_loud;
					Zeny -= 501000000;
					getitem 6024, 1;
					next;

					mes .@npc$;
					mes "Você pode trocar o diamante comigo de volta a qualquer momento.";
					mes "No entanto, haverá uma taxa de serviço de 1.000.000z.";
					mes "Agradeço pelos seus serviços.";
					emotion e_thx;
					close;
				}
				else {
					mes .@npc$;
					mes "Hm?";
					mes "Sinto muito, mas você não tem dinheiro o suficientes.";
					mes "Incluindo a taxa de serviço, um total de 501.000.000z é necessário para essa negociação.";
					mes "Verifique seu saldo de zeny e volte aqui quando puder.";
					emotion e_hmm;
					close;
				}
			}
			else {
				mes .@npc$;
				mes "Esta é realmente sua assinatura?";
				mes "Pode assinar novamente para confirmar?";
				next;
			}
		}
	case 2:
		mes .@npc$;
		mes "AAH. Você gostaria de trocar o seu ^FF82FF'Diamante de 17 Quilatos'^000000 por zenys?";
		emotion e_gasp;
		next;
		if (select("Sim:Não") == 2) {
			mes .@npc$;
			mes "Então, o que você deseja?..";
			mes ". . . . . ....";
			emotion e_dots;
			close;
		}

		mes .@npc$;
		mes "Entendo. Depois de receber a sua assinatura, ";
		mes "Você deve devolver o seu ^FF82FF'Diamante de 17 Quilates'^000000.";
		mes "Também não se esqueça que há uma taxa de serviço 1.000.000z!";
		next;
		
		mes .@npc$;
		mes "Como você se chama?";
		next;
		
		mes "[" + strcharinfo(0) + "]";
		mes "Eu me chamo " + strcharinfo(0) + ".";
		next;
		
		mes .@npc$;
		mes "Tudo bem, assine aqui.";
		next;
		
		while (1) {
			if (select("Assinar:Não Assinar") == 2) {
				mes .@npc$;
				mes "A negociação não pode ser concluída sem a sua assinatura.";
				close;
			}
			
			input .@charname$;
			if (.@charname$ == strcharinfo(0)) {
				mes .@npc$;
				mes "Recebi a sua assinatura.";
				mes "Agora só lhe resta me entregar o seu ^FF82FF'Diamante de 17 Quilates'^000000..";
				mes "Para você receber os seus ^0000FF499,000,000z^000000 de volta.";
				next;
				
				mes .@npc$;
				mes "Agradeço pelos seus serviços.";
				mes "Sempre que precisar de um ^FF82FF'Diamante de 17 Quilates'^000000,";
				mes "é só voltar com 500 milhões de zenys em mãos.";
				emotion e_thx;
				delitem 6024, 1;
				Zeny += 499000000;
				close;
			}
			else {
				mes .@npc$;
				mes "Esta é realmente sua assinatura?";
				mes "Pode assinar novamente para confirmar?";
				next;
			}
		}
	}
}

prontera,165,89,6	duplicate(NegociantedeDiamantes)	Negociante de Diamantes#1	1_M_MERCHANT
geffen,106,63,6	duplicate(NegociantedeDiamantes)	Negociante de Diamantes#2	1_M_MERCHANT
morocc,146,100,6	duplicate(NegociantedeDiamantes)	Negociante de Diamantes#3	1_M_MERCHANT
alberta,129,60,6	duplicate(NegociantedeDiamantes)	Negociante de Diamantes#4	1_M_MERCHANT
lighthalzen,163,65,6	duplicate(NegociantedeDiamantes)	Negociante de Diamantes#5	1_M_MERCHANT
rachel,106,142,6	duplicate(NegociantedeDiamantes)	Negociante de Diamantes#6	1_M_MERCHANT
payon,180,130,6	duplicate(NegociantedeDiamantes)	Negociante de Diamantes#7	1_M_MERCHANT