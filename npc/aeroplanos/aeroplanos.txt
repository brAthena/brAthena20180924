//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: aeroplanos.txt                         |
//   |--------------------------------------------------------|
//   | Criado por: Herc/brA Dev. Team                         |
//   |--------------------------------------------------------|
//   | Versões Herc/brA: 1.0/1.0                              |
//   |--------------------------------------------------------|
//   | Descrição: Sistema de aeroplanos.                      |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Cabeçalho brAthena [Axl]                           |
//   |--------------------------------------------------------|
//   | - Revisado [Jeeffrey]                                  |
//   \________________________________________________________/

airplane,243,73,0	script	#AeroplanoPortal-1	WARPNPC,1,1,{
	end;

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(0);
	end;

OnUnhide:
	enablenpc strnpcinfo(0);
	specialeffect EF_SUMMONSLAVE;
	end;

OnTouch:
	switch ($@airplanelocation) {
	case 0: warp "yuno",92,260; end;
	case 1: warp "einbroch",92,278; end;
	case 2: warp "lighthalzen",302,75; end;
	case 3: warp "hugel",181,146; end;
	}
}

airplane,243,29,0	duplicate(#AeroplanoPortal-1)	#AeroplanoPortal-2	WARPNPC,1,1

airplane,1,1,0	script	Aeroplano_Domestico	-1,{
	end;
OnInit:
	initnpctimer;
	end;
OnTimer20000:
	mapannounce "airplane","Nós estamos partindo para Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane","Em breve chegaremos a Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer60000:
	$@airplanelocation = 1;
	donpcevent "#AeroplanoPortal-1::OnUnhide";
	donpcevent "#AeroplanoPortal-2::OnUnhide";
	mapannounce "airplane","Bem-vindo a Einbroch. Tenha uma boa viagem.",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane","Atualmente nós estamos em Einbroch. O Aeroplano irá decolar em instantes.",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AeroplanoPortal-1::OnHide";
	donpcevent "#AeroplanoPortal-2::OnHide";
	mapannounce "airplane","O Aeroplano está decolando agora. Seu próximo destino é Lighthalzen.",bc_map,"0x70dbdb";
	end;
OnTimer100000:
	mapannounce "airplane","Nós estamos partindo para Lighthalzen.",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane","Em breve chegaremos a Lighthalzen.",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	$@airplanelocation = 2;
	donpcevent "#AeroplanoPortal-1::OnUnhide";
	donpcevent "#AeroplanoPortal-2::OnUnhide";
	mapannounce "airplane","Bem-vindo a Lighthalzen. Tenha uma boa viagem.",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane","Atualmente nós estamos em Lighthalzen. O Aeroplano irá decolar em instantes.",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AeroplanoPortal-1::OnHide";
	donpcevent "#AeroplanoPortal-2::OnHide";
	mapannounce "airplane","O Aeroplano está decolando agora. Seu próximo destino é Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer180000:
	mapannounce "airplane","Nós estamos partindo para Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer210000:
	mapannounce "airplane","Em breve chegaremos a Einbroch.",bc_map,"0x00FF00";
	end;
OnTimer220000:
	$@airplanelocation = 1;
	donpcevent "#AeroplanoPortal-1::OnUnhide";
	donpcevent "#AeroplanoPortal-2::OnUnhide";
	mapannounce "airplane","Bem-vindo a Einbroch. Tenha uma boa viagem.",bc_map,"0x00ff00";
	end;
OnTimer230000:
	mapannounce "airplane","Atualmente nós estamos em Einbroch. O Aeroplano irá decolar em instantes.",bc_map,"0x00ff00";
	end;
OnTimer240000:
	donpcevent "#AeroplanoPortal-1::OnHide";
	donpcevent "#AeroplanoPortal-2::OnHide";
	mapannounce "airplane","O Aeroplano está decolando agora. Seu próximo destino é Juno.",bc_map,"0xff8200";
	end;
OnTimer260000:
	mapannounce "airplane","Nós estamos partindo para Juno.",bc_map,"0xff8200";
	end;
OnTimer290000:
	mapannounce "airplane","Em breve chegaremos a Juno.",bc_map,"0xff8200";
	end;
OnTimer300000:
	$@airplanelocation = 0;
	donpcevent "#AeroplanoPortal-1::OnUnhide";
	donpcevent "#AeroplanoPortal-2::OnUnhide";
	mapannounce "airplane","Bem-vindo a Juno. Tenha uma boa viagem.",bc_map,"0xff8200";
	end;
OnTimer310000:
	mapannounce "airplane","Atualmente nós estamos em Juno. O Aeroplano irá decolar em instantes.",bc_map,"0xff8200";
	end;
OnTimer320000:
	donpcevent "#AeroplanoPortal-1::OnHide";
	donpcevent "#AeroplanoPortal-2::OnHide";
	mapannounce "airplane","O Aeroplano está decolando agora. Seu próximo destino é Hugel.",bc_map,"0xca4bf3";
	end;
OnTimer340000:
	mapannounce "airplane","Nós estamos partindo para Hugel.",bc_map,"0xca4bf3";
	end;
OnTimer370000:
	mapannounce "airplane","Em breve chegaremos a Hugel.",bc_map,"0xca4bf3";
	end;
OnTimer380000:
	$@airplanelocation = 3;
	donpcevent "#AeroplanoPortal-1::OnUnhide";
	donpcevent "#AeroplanoPortal-2::OnUnhide";
	mapannounce "airplane","Bem-vindo a Hugel. Tenha uma boa viagem.",bc_map,"0xca4bf3";
	end;
OnTimer390000:
	mapannounce "airplane","Atualmente nós estamos em Hugel. O Aeroplano irá decolar em instantes.",bc_map,"0xca4bf3";
	end;
OnTimer400000:
	donpcevent "#AeroplanoPortal-1::OnHide";
	donpcevent "#AeroplanoPortal-2::OnHide";
	mapannounce "airplane","O Aeroplano está decolando agora. Seu próximo destino é Juno.",bc_map,"0xff8200";
	end;
OnTimer420000:
	mapannounce "airplane","Nós estamos partindo para Juno.",bc_map,"0xff8200";
	end;
OnTimer450000:
	mapannounce "airplane","Em breve chegaremos a Juno.",bc_map,"0xff8200";
	end;
OnTimer460000:
	$@airplanelocation = 0;
	donpcevent "#AeroplanoPortal-1::OnUnhide";
	donpcevent "#AeroplanoPortal-2::OnUnhide";
	mapannounce "airplane","Bem-vindo a Juno. Tenha uma boa viagem.",bc_map,"0xff8200";
	end;
OnTimer470000:
	mapannounce "airplane","Atualmente nós estamos em Juno. O Aeroplano irá decolar em instantes.",bc_map,"0xff8200";
	end;
OnTimer480000:
	donpcevent "#AeroplanoPortal-1::OnHide";
	donpcevent "#AeroplanoPortal-2::OnHide";
	mapannounce "airplane","O Aeroplano está decolando agora. Seu próximo destino é Einbroch.",bc_map,"0x00ff00";
	stopnpctimer;
	initnpctimer;
}

airplane,240,64,5	script	Saída#airplane1a::ExitAirplane	4_BOARD3,{
	end;
}
airplane,247,64,5	duplicate(ExitAirplane)	Saída#airplane1b	4_BOARD3
airplane,240,40,1	duplicate(ExitAirplane)	Saída#airplane2a	4_BOARD3
airplane,247,40,1	duplicate(ExitAirplane)	Saída#airplane2b	4_BOARD3

airplane,100,69,3	script	Tripulação do Aeroplano#ein-1	4_M_EIN_SOLDIER,{
	mes "[Tripulação do Aeroplano]";
	mes "Se já pousamos no";
	mes "seu destino e você";
	mes "gostaria de sair do Aeroplano,";
	mes "por favor use as";
	mes "escadas a frente. Obrigado";
	mes "por sua ajuda.";
	close;
}

airplane,64,94,1	script	Criança de Umbala#ein_p	4_M_UMKID,{
	emotion e_swt2;
	mes "[Criança]";
	if (event_umbala >= 3) {
		mes "Uau, mãe!";
		mes "V-veja isso!";
		mes "Nós estamos voando! N-nós estamos...";
		mes "Estamos conhecendo o céu!";
	} else {
		mes "Makumalagu!";
		mes "Saampa joojimbo";
		mes "kaku na jedi Solo.";
		mes "Bwahahahahahahaah!";
	}
	close;
}

airplane,66,93,3	script	Senhora de Umbala#ein_p	4_F_UMWOMAN,{
	emotion e_dots;
	mes "[Senhora]";
	if (event_umbala >= 3) {
		mes "Shush...";
		mes "Querido, comporte-se~";
		mes "Não se anime tanto";
		mes "enquanto não sairmos";
		mes "deste lugar público!";
	} else {
		mes "Chooktu!";
		mes "Sacraup matii!";
		mes "Shaka gurftalfi";
		mes "huntiki manjoo!";
	}
	close;
}

airplane,71,91,7	script	Homem de Umbala#ein_p	4_M_UMSOLDIER,{
	if (event_umbala >= 3) {
		mes "Ei você~";
		mes "Pela sua aparência, percebo que você consegue me entender.";
		mes "^333333*Legal...!*^000000";
		next;
		emotion e_pif;
		mes "[Chrmlim]";
		mes "Tenho ajudado a equipe do Aeroplano auxiliando seus Tripulantes que têm medo de altura os levanto para pular de bungee jumping em Umbala.";
		mes "Legal, em?";
		next;
		mes "[Chrmlim]";
		mes "Mas...";
		mes "Alguns deles não podem superar seus medos.";
		mes "Devem participar de algumas experiências, ^333333em Nifflheim^000000.";
	} else {
		mes "[Chrmlim]";
		mes "Bajoo ga";
		mes "nukta Airship.";
		next;
		mes "[Chrmlim]";
		mes "...";
		mes "......";
		next;
		emotion e_pif;
		mes "[Chrmlim]";
		mes "Shabala moow bajama";
		mes "Airship kulaha googoona ";
		mes "salu. Dama, kookoo na nu";
		mes "yukuta. Um, fashuku na ret!";
	}
	close;
}

airplane,250,58,2	script	Equipe do Aeroplano#airplane	1_F_02,{
	// Adição da Quest Hugel
	if (hg_ma1 == 3) {
		mes "[Assistente do Aeroplano]";
		mes "Bem-vind"+(Sex?"o":"a")+" ao Aeroplano.";
		mes "Como posso ajudá-lo?";
		next;
		select("Você tem um passageiro chamado Thierry?");
		mes "[Assistente do Aeroplano]";
		mes "Me desculpe, mas eu não conheço nenhum dos passageiros pelo nome.";
		close;
	}
	// Final da Quest Hugel
	mes "[Assistente do Aeroplano]";
	mes "Bem-vind"+(Sex?"o":"a")+" ao Aeroplano.";
	mes "Como posso lhe ajudar??";
	next;
	switch (select("Embarcar no Aeroplano:Cabine do Capitão:Atrativos:Cancelar")) {
	case 1:
		mes "[Assistente do Aeroplano]";
		mes "Quando você ver o aviso anunciando que chegamos no seu destino desejado, por favor, use uma das saídas localizadas nas partes norte e sul do Aeroplano.";
		next;
		mes "[Assistente do Aeroplano]";
		mes "Caso você perca o momento de sua parada, não se preocupe.";
		mes "O Aeroplano está constantemente em uma rota única e você terá outra chance para descer no destino exato que quiser.";
		close;
	case 2:
		mes "[Assistente do Aeroplano]";
		mes "A Cabine do Capitão fica na parte frontal do Aeroplano.";
		mes "Lá, você pode conhecer o capitão e o piloto do Aeroplano.";
		close;
	case 3:
		mes "[Assistente do Aeroplano]";
		mes "O Aeroplano possui vários mini-jogos para o entretenimento de todos os passageiros.";
		mes "Você está convidad"+(Sex?"o":"a")+" a testar sua sorte no Mini-Jogos do Aeroplano~";
		close;
	case 4:
		mes "[Assistente do Aeroplano]";
		mes "Bem, desejo-lhe uma boa estadia durante o vôo em nosso Aeroplano.";
		mes "Obrigado e tenha um bom dia.";
		close;
	}
}

airplane,80,71,2	script	Zerta#01airplane	4_M_BUDDHIST,{
	mes "[Zerta]";
	mes "Ah, olá aventureiro.";
	mes "Eu estou atualmente em uma jornada sacrada, oferecendo sacrifícios por todo o continente de Rune-Midgard.";
	close;
}

airplane,65,63,4	script	Maelin#01airplane	4_F_06,{
	mes "[Maelin]";
	mes "Hum, o Aeroplano vai até Lutie, não é?";
	mes "Eu tenho esperado durante um longo tempo, mas até agora não tenho tido notícias de Lutie.";
	close;
}

airplane,72,34,6	script	Aanos#01airplane	4_F_GON,{
	mes "[Aanos]";
	mes "Ah uau~";
	mes "O céu parece estar tão diferente e lindo hoje.";
	close;
}

airplane,221,158,2	script	Piloto#airplane	4_M_EIN_SOLDIER,{
	// Adição da Quest Hugel
	if (hg_ma1 == 3) {
		mes "[Piloto]";
		mes "Espero que o drink esteja fresco e saboroso.";
		mes "Bebidas são a minha vida! Beber me dá energias!";
		mes "Eu não vivo sem bebidas!";
		next;
		mes "[Piloto]";
		mes "Mas! Dirigir sob influência da bebida não é bom.";
		mes "Porém! Eu preciso estar bebendo mais e mais!";
		emotion e_sob;
		next;
		select("Você conhece um passageiro chamado Thierry?");
		mes "[Piloto]";
		mes "Este uniforme é realmente bonito, mas não gosto de usá-lo enquanto estou no Aeroplano.";
		next;
		mes "[Piloto]";
		mes "...";
		mes "......";
		mes "Ninguém nunca realmente entrou neste quarto.";
		mes "E como sou o capitão. Eu poderia simplesmente ficar de cuecas.";
		next;
		emotion e_omg;
		mes "[Piloto]";
		mes "Ah!? O que é isto!";
		next;
		mes "- ...Ele não está ouvindo tudo que você fala. -";
		close;
	}
	// Final da Quest Hugel
	switch (rand(1,4)) {
	case 1:
		mes "[Piloto]";
		mes "Faz bastante tempo desde que eu esperimentei um bom e gelado álcool.";
		mes "Mas a profissão requer que eu esteja sempre sóbrio!";
		next;
		mes "[Piloto]";
		mes "Sempre beba com responsabilidade!";
		mes "Ainda sim, não me lembro a última vez que entrei de férias.";
		mes "Sim, uma bebida, uma TV e tudo ficará ótimo.";
		emotion e_sob;
		close;
	case 2:
		mes "[Piloto]";
		mes "Cara, o clima está ótimo hoje.";
		mes "Brilho, o céu está aberto com uma boa visibilidade e segurança, um voo despreocupado.";
		close;
	case 3:
		mes "[Piloto]";
		mes "Você sabe, seu capitão é um homem respeitado.";
		mes "Ele e seu irmão são atualmente os mais conhecidos na indústria aérea.";
		mes "Quem diria que nos tornariamos capitões de voo tão bons?";
		next;
		mes "[Piloto]";
		mes "Apenas entre nós, tenho que dizer a você, o Papai Noel foi para algum lugar com renas e elfos que trabalham para ele.";
		mes "Aquele cara é um gênio!";
		close;
	default:
		mes "[Piloto]";
		mes "Você sabe, toda essa coisa de pilotagem no ar, é algo bastante recente, você sabe não é?";
		mes "Sim, temos um Aeroplano voando a uma incrível velocidade.";
		next;
		emotion e_omg;
		mes "[Piloto]";
		mes "Ainda assim, há sempre algo sério, realmente acontecendo.";
		mes "Quer dizer, estavamos treinando enquanto o Aeroplano estava sendo inventado.";
		mes "Não é uma loucura?!";
		close;
	}
}

airplane,50,66,5	script	Vendedor de Maçãs#airplane	4_M_04,{
	mes "[Vendedor de Maçãs]";
	mes "Bem-vindo a Banca de Frutas onde você pode comprar Maçãs ou juntar ingredientes para fazer o Suco de Maçã.";
	next;
	switch (select("Comprar Maçãs.:Fazer Suco de Maçã.:Por que você está aqui?:Cancelar.")) {
	case 1:
		mes "[Vendedor de Maçãs]";
		mes "Por favor me diga a quantidade de Maçãs que você deseja comprar.";
		mes "Cada Maçã custa 15 zeny e você pode comprar no máximo 500 por vez.";
		mes "Digite '0' para cancelar sua compra.";
		next;
		while (1) {
			input .@input;
			.@pay = .@input * 15;
			if (.@input == 0) {
				mes "[Vendedor de Maçãs]";
				mes "Obrigado por visitar minha banca.";
				mes "Adeus!";
				mes "Venha quando você";
				mes "sentir que precisa de uma Maçã.";
				close;
			} else if (.@input < 1 || .@input > 500) {
				mes "[Vendedor de Maçãs]";
				mes "Você escolheu uma quantia muito baixa ou muito alta, o valor mínimo é 1 e o máximo são 500.";
				mes "Por favor digite o número de maçãs que você deseja comprar.";
				next;
			} else {
				mes "[Vendedor de Maçãs]";
				mes "Um total de ^FF0000" + .@input + "^000000 Maçãs irá custar a você ^FF0000" + .@pay + " Zeny^000000 zeny.";
				mes "Você gostaria de continuar?";
				next;
				if (select("Sim:Não") == 2) {
					mes "[Vendedor de Maçãs]";
					mes "Obrigado por visitar minha banca.";
					mes "Adeus!";
					mes "Venha quando você sentir que precisa de uma Maçã.";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[Vendedor de Maçãs]";
			mes "Me desculpe, mas você não possui a quantidade de zenys necessária para comprar essas Maçãs.";
			mes "Por favor verifique a quantidade de zenys que você possui e quantas Maçãs quer comprar.";
			close;
		} else if (checkweight(512,.@input) == 0) {
			mes "[Vendedor de Maçãs]";
			mes "Humm, eu acho que você não pode carregar mais itens em seu inventário, assim não pode comprar mais Maçãs.";
			mes "Você poderia liberar um pouco de espaço em seu inventário?";
			close;
		} else {
			Zeny -= .@pay;
			getitem 512,.@input; //Maçã
			mes "[Vendedor de Maçãs]";
			mes "Obrigado por visitar minha banca.";
			mes "Adeus!";
			mes "Venha quando você";
			mes "sentir que precisa de uma Maçã ~.";
			close;
		}
	case 2:
		mes "[Vendedor de Maçãs]";
		mes "Tudo bem, irei precisar de ^FF00003 Maçãs e 1 Garrafa Vazia^000000 para criar 1 Suco de Maçã para você.";
		mes "Você deseja continuar?";
		next;
		switch (select("Sim:Não")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[Vendedor de Maçãs]";
				mes "Me desculpe,mas você não possui os materiais suficientes para criar uma Garrafa de Suco de Maçã.";
				mes "Lembre-se, irei precisar de 3 Maçãs e 1 Garrafa Vazia.";
				close;
			} else {
				mes "Obrigado, por favor espere um momento.";
				next;
				mes "^3355FF*Moer moer*";
				mes "*Moer moer*";
				mes "*Clang...!*^000000";
				next;
				delitem 512,3; // Maçã
				delitem 713,1; // Garrafa Vazia
				getitem 531,1; // Suco de Maçã
				mes "[Vendedor de Maçãs]";
				mes "Aqui está~";
				mes "Espero que você goste!";
				mes "Por favor fique a vontade para comprar Maçãs ou fazer Sucos de Maçãs quando você precisar, aventureiro~";
				close;
			}
		case 2:
			mes "[Vendedor de Maçãs]";
			mes "Obrigado por visitar minha banca.";
			mes "Adeus!";
			mes "Venha quando você sentir que precisa de uma Maçã ~.";
			close;
		}
	case 3:
		mes "[Vendedor de Maçãs]";
		mes "Eu costumava ser um andarilho vagabundo, até que um dia, tirei uma soneca e algo atingiu minha cabeça e acordei do meu sono tranquilo.";
		next;
		mes "[Vendedor de Maçãs]";
		mes "Acontece que eu estava dormindo embaixo de uma Macieira e foi quando uma maçã caiu e atingiu minha cabeça.";
		mes "Eu estava morrendo de fome e estava prestes a comer aquela Maçã...";
		next;
		mes "[Vendedor de Maçãs]";
		mes "Mas de repente, Kain, meu antigo amigo desde os tempos de mineração, me pediu para ajudá-lo aqui no Aeroplano.";
		mes "Então eu aceitei, e foi lá que encontrei pessoas jogando um Jogo de Dados.";
		next;
		mes "[Vendedor de Maçãs]";
		mes "Estava entediado e curioso e acabei apostando algumas simples Maçãs no jogo dos dados.";
		mes "Mas por alguma razão, eu tive uma incrível sorte.";
		mes "Uma maçã se multiplicou em duas... ";
		next;
		mes "[Vendedor de Maçãs]";
		mes "Duas tornaram-se quatro e antes que eu percebesse, eu já tinha feito uma banca de Maçãs!";
		mes "Ganhei tantas maçãs, e eu acabei começando meu próprio negócio aqui no Aeroplano.";
		mes "Estranho, não acha?";
		next;
		mes "[Vendedor de Maçãs]";
		mes "Maçãs são boas para você.";
		mes "Elas são concerteza muito boas para mim.";
		mes "Hahahahaah~!";
		close;
	case 4:
		mes "[Vendedor de Maçãs]";
		mes "Obrigado por utilizar a minha loja.";
		mes "Tchau~";
		close;
	}
}

//============================================================ 
//= Rota Internacional do Aeroplano
//= ... -> Izlude -> Juno -> Rachel -> ...
//============================================================ 

airplane_01,243,73,0	script	#AeroplanoPortal-3	WARPNPC,1,1,{
	end;
	
OnTouch:
	switch ($@airplanelocation2) {
	case 0: warp "ra_fild12",292,204; end;
	case 1:
		if (RENEWAL)
			warp "izlude",200,73;
		else
			warp "izlude",200,56;
		end;
	case 2: warp "yuno",12,261; end;
	}

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(0);
	end;
	
OnUnhide:
	enablenpc strnpcinfo(0);
	specialeffect EF_SUMMONSLAVE;
	end;
}

airplane_01,243,29,0	duplicate(#AeroplanoPortal-3)	#AeroplanoPortal-4	WARPNPC,1,1

airplane_01,1,1,0	script	Aeroplano_Internacional	-1,{
	end;
OnInit:
OnEnable:
	initnpctimer;
	end;
OnTimer25000:
	mapannounce "airplane_01","Nós estamos partindo para Izlude.",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane_01","Em breve chegaremos a Izlude.",bc_map,"0x00ff00";
	end;
OnTimer60000:
	$@airplanelocation2 = 1;
	donpcevent "#AeroplanoPortal-3::OnUnhide";
	donpcevent "#AeroplanoPortal-4::OnUnhide";
	mapannounce "airplane_01","Bem-vindo a Izlude. Tenha uma boa viagem.",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane_01","Atualmente nós estamos em Izlude. O Aeroplano irá decolar em instantes.",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AeroplanoPortal-3::OnHide";
	donpcevent "#AeroplanoPortal-4::OnHide";
	mapannounce "airplane_01","O Aeroplano está decolando agora. Seu próximo destino é Juno.",bc_map,"0x70dbdb";
	end;
OnTimer105000:
	mapannounce "airplane_01","Nós estamos partindo para Juno.",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane_01","Em breve chegaremos a Juno.",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	$@airplanelocation2 = 2;
	donpcevent "#AeroplanoPortal-3::OnUnhide";
	donpcevent "#AeroplanoPortal-4::OnUnhide";
	mapannounce "airplane_01","Bem-vindo a Juno. Tenha uma boa viagem.",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane_01","Atualmente nós estamos em Juno. O Aeroplano irá decolar em instantes.",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AeroplanoPortal-3::OnHide";
	donpcevent "#AeroplanoPortal-4::OnHide";
	mapannounce "airplane_01","O Aeroplano está decolando agora. Seu próximo destino é Rachel.",bc_map,"0xFF8200";
	end;
OnTimer185000:
	mapannounce "airplane_01","Nós estamos partindo para Rachel.",bc_map,"0xFF8200";
	end;
OnTimer210000:
	mapannounce "airplane_01","Em breve chegaremos a Rachel.",bc_map,"0xFF8200";
	end;
OnTimer220000:
	$@airplanelocation2 = 0;
	donpcevent "#AeroplanoPortal-3::OnUnhide";
	donpcevent "#AeroplanoPortal-4::OnUnhide";
	mapannounce "airplane_01","Bem-vindo a Rachel. Tenha uma boa viagem.",bc_map,"0xFF8200";
	end;
OnTimer230000:
	mapannounce "airplane_01","Atualmente nós estamos em Rachel. The Airship will take off shortly.",bc_map,"0xFF8200";
	end;
OnTimer240000:
	donpcevent "#AeroplanoPortal-3::OnHide";
	donpcevent "#AeroplanoPortal-4::OnHide";
	mapannounce "airplane_01","O Aeroplano está decolando agora. Seu próximo destino é Izlude.",bc_map,"0x00ff00";
	stopnpctimer;
	++.moninv;
	if (.moninv == 7) {
		if (rand(1,3) == 3) {
			donpcevent "Aeroplano_Internacional::OnEnable";
			end;
		}
		.moninv = 0;
	}
	initnpctimer;
	end;
}

airplane_01,240,64,5	script	Saída#airplane_011a::ExitAirplane01	4_BOARD3,{
	end;
}
airplane_01,247,64,5	duplicate(ExitAirplane01)	Saída#airplane_011b	4_BOARD3
airplane_01,240,40,1	duplicate(ExitAirplane01)	Saída#airplane_012a	4_BOARD3
airplane_01,247,40,1	duplicate(ExitAirplane01)	Saída#airplane_012b	4_BOARD3

airplane_01,250,58,2	script	Equipe do Aeroplano#airplane01	1_F_02,{
	mes "[Assistente do Aeroplano]";
	mes "Bem-vind"+(Sex?"o":"a")+" ao Aeroplano.";
	mes "Como posso lhe ajudar??";
	next;
	switch (select("Embarcar no Aeroplano:Cabine do Capitão:Atrativos:Cancelar")) {
	case 1:
		mes "[Assistente do Aeroplano]";
		mes "Quando você ver o aviso anunciando que chegamos no seu destino desejado, por favor, use uma das saídas localizadas nas partes norte e sul do Aeroplano.";
		next;
		mes "[Assistente do Aeroplano]";
		mes "Caso você perca o momento de sua parada, não se preocupe.";
		mes "O Aeroplano está constantemente em uma rota única e você terá outra chance para descer no destino exato que quiser.";
		close;
	case 2:
		mes "[Assistente do Aeroplano]";
		mes "A Cabine do Capitão fica na parte frontal do Aeroplano.";
		mes "Lá, você pode conhecer o capitão e o piloto do Aeroplano.";
		close;
	case 3:
		mes "[Assistente do Aeroplano]";
		mes "O Aeroplano possui vários mini-jogos para o entretenimento de todos os passageiros.";
		mes "Você está convidad"+(Sex?"o":"a")+" a testar sua sorte no Mini-Jogos do Aeroplano~";
		close;
	case 4:
		mes "[Assistente do Aeroplano]";
		mes "Bem, desejo-lhe uma boa estadia durante o vôo em nosso Aeroplano.";
		mes "Obrigado e tenha um bom dia.";
		close;
	}
}

airplane_01,50,66,5	script	Vendedor de Maçãs#air01	4_M_04,{
	if (checkweight(1201,1) == 0) {
		mes "- Espere um minuto !! -";
		mes "- Atualmente você está carregando -";
		mes "- muitos itens com você. -";
		mes "- Por favor volte novamente -";
		mes "- após perder um pouco de seu peso. -";
		close;
	}
	mes "[Vendedor de Maçãs]";
	mes "Bem-vindo a Banca de Frutas onde você pode comprar Maçãs ou juntar ingredientes para fazer o Suco de Maçã.";
	next;
	switch (select("Comprar Maçãs.:Fazer Suco de Maçã.:Cancelar.")) {
	case 1:
		mes "[Vendedor de Maçãs]";
		mes "Por favor me diga a quantidade de Maçãs que você deseja comprar.";
		mes "Cada Maçã custa 15 zeny e você pode comprar no máximo 500 por vez.";
		mes "Digite '0' para cancelar sua compra.";
		next;
		while (1) {
			input .@input;
			.@pay = .@input * 15;
			if (.@input == 0) {
				mes "[Vendedor de Maçãs]";
				mes "Obrigado por visitar minha banca.";
				mes "Adeus!";
				mes "Venha quando você";
				mes "sentir que precisa de uma Maçã.";
				close;
			} else if (.@input < 1 || .@input > 500) {
				mes "[Vendedor de Maçãs]";
				mes "Você escolheu uma quantia muito baixa ou muito alta, o valor mínimo é 1 e o máximo são 500.";
				mes "Por favor digite o número de maçãs que você deseja comprar.";
				next;
			} else {
				mes "[Vendedor de Maçãs]";
				mes "Um total de ^FF0000" + .@input + "^000000 Maçãs irá custar a você ^FF0000" + .@pay + " Zeny^000000 zeny.";
				mes "Você gostaria de continuar?";
				next;
				if (select("Sim:Não") == 2) {
					mes "[Vendedor de Maçãs]";
					mes "Obrigado por visitar minha banca.";
					mes "Adeus!";
					mes "Venha quando você sentir que precisa de uma Maçã.";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[Vendedor de Maçãs]";
			mes "Me desculpe, mas você não possui a quantidade de zenys necessária para comprar essas Maçãs.";
			mes "Por favor verifique a quantidade de zenys que você possui e quantas Maçãs quer comprar.";
			close;
		} else if (checkweight(512,.@input) == 0) {
			mes "[Vendedor de Maçãs]";
			mes "Humm, eu acho que você não pode carregar mais itens em seu inventário, assim não pode comprar mais Maçãs.";
			mes "Você poderia liberar um pouco de espaço em seu inventário?";
			close;
		} else {
			Zeny -= .@pay;
			getitem 512,.@input; // Maçã
			mes "[Vendedor de Maçãs]";
			mes "Obrigado por visitar minha banca.";
			mes "Adeus!";
			mes "Venha quando você";
			mes "sentir que precisa de uma Maçã ~.";
			close;
		}
	case 2:
		mes "[Vendedor de Maçãs]";
		mes "Tudo bem, irei precisar de ^FF00003 Maçãs e 1 Garrafa Vazia^000000 para criar 1 Suco de Maçã para você.";
		mes "Você deseja continuar?";
		next;
		switch (select("Sim:Não")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[Vendedor de Maçãs]";
				mes "Me desculpe,mas você não possui os materiais suficientes para criar uma Garrafa de Suco de Maçã.";
				mes "Lembre-se, irei precisar de 3 Maçãs e 1 Garrafa Vazia.";
				close;
			} else {
				mes "[Vendedor de Maçãs]";
				mes "Obrigado, por favor espere um momento.";
				next;
				mes "^3355FF*Moer moer*";
				mes "*Moer moer*";
				mes "*Clang...!*^000000";
				next;
				delitem 512,3; // Maçã
				delitem 713,1; // Garrafa Vazia
				getitem 531,1; // Suco de Maçã
				mes "[Vendedor de Maçãs]";
				mes "Aqui está~";
				mes "Espero que você goste!";
				mes "Por favor fique a vontade para comprar Maçãs ou fazer Sucos de Maçãs quando você precisar, aventureiro~";
				close;
			}
		case 2:
			mes "[Vendedor de Maçãs]";
			mes "Obrigado por visitar minha banca.";
			mes "Adeus!";
			mes "Venha quando você sentir que precisa de uma Maçã ~.";
			close;
		}
	case 3:
		mes "[Vendedor de Maçãs]";
		mes "Obrigado por utilizar a minha loja.";
		mes "Tchau~";
		close;
	}
}

airplane_01,221,158,2	script	Piloto#airplane_01	4_M_EIN_SOLDIER,{
	switch (rand(1,4)) {
	case 1:
		mes "[Piloto]";
		mes "Longitude, 131 graus leste.";
		mes "Latitude, 37 graus norte.";
		mes "Estamos no curso certo, capitão.";
		close;
	case 2:
		mes "[Piloto]";
		mes "Parece ser um dia com muita neblina.";
		mes "É Sempre difícil voar quando os céus não estão limpos.";
		mes "Acho que precisaremos utilizar o radar.";
		close;
	case 3:
		mes "[Piloto]";
		mes "O capitão é um excelente homem e não consigo pensar em ninguém melhor para comandar este Aeroplano.";
		mes "E ainda, é realmente, um piloto talentoso.";
		next;
		mes "[^ff0000Tarlock^000000]";
		mes "^ff0000Eii...!^000000";
		mes "^ff0000Menos papo^000000";
		mes "^ff0000e mais pilotagem!^000000";
		next;
		mes "[Piloto]";
		mes "C-certo, senhor!";
		mes "(Entendeu?)";
		close;
	default:
		mes "[Piloto]";
		mes "Este uniforme é bem elegante, mas é muito chamativo para usar pelo Aeroplano.";
		next;
		mes "[Piloto]";
		mes "...";
		mes "......";
		mes "Realmente ninguém veio a este quarto.";
		mes "E o capitão É um alce.";
		//mes "Eu poderia somente tirar minhas cuecas."; <-- ????
		next;
		emotion e_omg;
		mes "[Piloto]";
		mes "Oh...! Olá amigo!";
		mes "E-e-está aproveitando o vôo?!";
		close;
	}
}

airplane_01,83,61,2	script	Dianne#01airplane_01	1_F_MARIA,2,2,{
	mes "[Dianne]";
	mes "É tão estranho!";
	mes "Eu venho visitar o Capitão do Aeroplano e tudo que vejo é esse alce estúpido.";
	mes "Oh! Não esperava por essa...";
	close;

OnTouch:
	emotion e_sob;
	end;
}

airplane_01,69,63,2	script	Mendel#01airplane_01	1_M_JOBTESTER,{
	mes "[Mendel]";
	mes "Como eu esperava, as comidas do vôo são no máximo 3 estrelas.";
	mes "*Harrrumph*";
	mes "Eu deveria mesmo ter trazido meu cozinheiro então poderia apreciar uma comida de verdade.";
	close;
}

airplane_01,71,31,2	script	Espadachim Shimizu#air_01	1_M_MOC_LORD,{
	mes "[Espadachim Shimizu]";
	mes "Finalmente, depois de cinco anos esperando...";
	mes "Eu terei minha vingança!";
	next;
	mes "[Espadachim Shimizu]";
	mes "Eu só...";
	mes "Só tenho que me certificar de que não perderei minha parada.";
	mes "Mas logo, muito breve, eu terei minha vingança.";
	close;
}

//============================================================ 
//= Desafio de Digitação
//============================================================ 
airplane_01,32,61,4	script	Nils#ein	1_M_03,1,1,{
	mes "[Nils]";
	mes "Bem-vind"+(Sex?"o":"a")+" ao ^ff0000Desafio de Escrita de RO^000000.";
	mes "Você gostaria de jogar um rápido jogo de escrita?";
	next;
	switch (select("Jogar ^ff0000Desafio de Escrita de RO^000000:Informações:Ver recordes:Cancelar")) {
	case 1:
		mes "[Nils]";
		mes "Ok, nós temos um novo desafio!";
		mes "Digite o seguinte texto o mais rápido que puder sem cometer nenhum erro!";
		mes "Vamos começar~!";
		setarray .@line1_1$[0],	"^3cbcbccallipygian salacius lascivious^000000",
					"^3cbcbcPelo poder de^000000",
					"^0000ffthkelfkskeldmsiejdlslehfndkelsheidl^000000",
					"^3cbcbcburrdingdingdingdilidingdingdingphoohudaamb^000000",
					"^3cbcbcCoboman no chikara-yumei na^000000",
					"^3cbcbcEu sou o rei dos esquisitos! Now^000000",
					"^3cbcbcVocê não me deu escolha. Eu aposto que^000000";
		setarray .@line1_2$[0],	"^3cbcbclicentious prurient concupiscent^000000",
					"^3cbcbcp-po-poi-po-poi-poin-poing^000000",
					"^3cbcbcskemd^000000",
					"^3cbcbcandoorabambarambambambambamburanbamding^000000",
					"^3cbcbcchikara-daiookii na chikara da ze!^000000",
					"^3cbcbcvocê conhece meu verdadeiro poder. Obey~!^000000",
					"^3cbcbchora de revelar meu segredo...^000000";
		setarray .@line1_3$[0],	"",
					"^3cbcbcDEUS-POING. EU NUNCA PERCO!^000000",
					"",
					"",
					"^3cbcbcCOBO LIGADO^000000",
					"",
					"";
		setarray .@word1$[0],	"callipygian salacius lascivious licentious prurient concupiscent",
					"Pelo poder de p-po-poi-po-poi-poin-poing DEUS-POING. EU NUNCA PERCO!",
					"thkelfkskeldmsiejdlslehfndkelsheidlskemd",
					"burrdingdingdingdilidingdingdingphoohudaambandoorabambarambambambambamburanbamding",
					"Coboman no chikara-yumei na chikara-daiookii na chikara da ze! COBO ON",
					"Eu sou o rei dos esquisitos! Agora você conhece meu verdadeiro poder. Obey~!",
					"Você não me deu escolha. Eu aposto que é hora de revelar meu poder...";
		setarray .@line2_1$[0],	"^3cbcbcuNflAPPaBLe LoVaBLe SeCreTs AnD^000000",
					"^ff1493VeLoCiDaDe Da LuZ ViRaR a EsQuErDa^000000",
					"^ff1493hfjdkeldjsieldjshfjdjeiskdlefvbd^000000",
					"^ff1493burapaphuralanderamduanbatuhiwooi^000000",
					"^ff1493acreditar na sorte do amor suar careta correr^000000",
					"^800080abriR, Abrir!abr1r.abrirAbrirAbr1rAbRir4brir^000000",
					"^3cbcbcfReeDoM ecstAcy JoUrnaliSm ArMplt^000000";
		setarray .@line2_2$[0],	"^3cbcbcboLiChe cOnTrA O kArmA JoVem^000000",
					"^ff1493CeRtO QuEiMe VeRdAdEiRo GaNgSteR bEijo^000000",
					"",
					"^ff1493kabamturubamdingding^000000",
					"^ff1493folktale rodimus optimus bumblebee^000000",
					"^800080`abrir'4Brir abrir? abrir!111ABRAMESMO^000000",
					"^3cbcbcDesCobrir dOr de CaBeça RaiosLunArES jUsTiçA^000000";
		setarray .@word2$[0],	"iNbATíVel AmÁvEl SeGrEdOs E boLiCHE cONtra o CarMA dos JoVeMs",
					"VeLoCiDaDe Da LuZ ViRaR a EsQuErDa QuEiMe VeRdAdEiRo GaNgSteR bEijo",
					"hfjdkeldjsieldjshfjdjeiskdlefvbd",
					"burapaphuralanderamduanbatuhiwooikabamturubamdingding",
					"acreditar na sorte do amor suar careta correr conto-de-fadas rodimus optimus bumblebee",
					"abriR, Abrir!abr1r.abrirAbrirAbr1rAbRir4brir`abrir'4Brir abrir? abrir!111ABRAMESMO",
					"lIberDadE ecstAsy JoRnAlIsMo ArMplt DesCobrir dOr de CaBeça RaiosLunArES jUsTiçA";
		setarray .@letters[0],	1300,
					1250,
					1180,
					1380,
					1740,
					1440,
					1450;
		.@wordtest = rand(7);
		next;
		mes "[Nils]";
		mes .@line1_1$[.@wordtest];
		mes .@line1_2$[.@wordtest];
		mes .@line1_3$[.@wordtest];
		.@start_time = gettime(HOUR)*60*60 + gettime(MINUTE)*60 + gettime(SECOND);
		next;
		input .@save1$;
		.@end_time = gettime(HOUR)*60*60 + gettime(MINUTE)*60 + gettime(SECOND);
		.@total_time = .@end_time - .@start_time;
		mes "[Nils]";
		mes .@line2_1$[.@wordtest];
		mes .@line2_2$[.@wordtest];
		.@start_time = gettime(HOUR)*60*60 + gettime(MINUTE)*60 + gettime(SECOND);
		next;
		input .@save2$;
		.@end_time = gettime(HOUR)*60*60 + gettime(MINUTE)*60 + gettime(SECOND);
		.@total_time = .@total_time + (.@start_time - .@end_time);
		.@tasoo = (.@letters[.@wordtest] / .@total_time) * 6;
		if ((.@save1$ == .@word1$[.@wordtest]) && (.@save2$ == .@word2$[.@wordtest])) {
			mes "[Nils]";
			mes "Seu recorde é ^ff0000" + .@total_time + " segundos^000000 e o total de letras são " + .@tasoo + ".";
			next;
			if (.@tasoo >= 1300) {
				mes "[Nils]";
				mes "Humm, não é possível humanamente falando, a menos que copie e cole toda a sentença.";
				mes "Por favor, jogue novamente.";
				close;
			}
			if (.@tasoo >= $050320_ein_typing) {
				mes "[Nils]";
				mes "O recorde anterior foi feito por ^0000ff" + $050320_minus1_typing$ + "^000000 com o total de ^0000ff" + $050320_ein_typing + "^000000 letras.";
				mes "Porém, ^ff0000" + strcharinfo(0) + "^000000, você conseguiu batê-lo.";
				mes "Parabéns!";
				$050320_minus1_typing$ = strcharinfo(0);
				$050320_ein_typing = .@tasoo;
				close;
			} else {
				mes "[Nils]";
				mes "^0000ff" + $050320_minus1_typing$ + "^000000 esse é o recorde atual conquistado com um total de ^0000ff" + $050320_ein_typing + "^000000 letras.";
				mes "Tente bater esse recorde na próxima~";
				close;
			}
		} else {
			mes "[Nils]";
			mes "Aaah...";
			mes "Me desculpe, mas você digitou o texto incorreto...";
			close;
		}
	case 2:
		mes "[Nils]";
		mes "O ^ff0000Desafio de Digitação de RO^000000 é um jogo onde você deve digitar o texto exibido o mais rápido que você puder.";
		mes "O nome do jogador que possuir o recorde será gravado nos registros.";
		mes "Se você procura fama, essa é sua chance!";
		next;
		mes "[Nils]";
		mes "Eu apenas gostaria de dizer que você deve digitar todo o texto em uma simples linha que será apresentada a você.";
		mes "Então não pressione uma tecla, apenas clique em 'OK.'";
		close;
	case 3:
		mes "[Nils]";
		mes "^0000ff" + $050320_minus1_typing$ + "^000000 é atualmente o recordista com o total de ^0000ff" + $050320_ein_typing + "^000000 letras.";
		mes "Tente quebrar o recorde na próxima vez~";
		close;
	case 4:
		mes "[Nils]";
		mes "Sinta-se livre para voltar quando quiser e jogar o ^ff0000Desafio de Digitação de RO.^000000";
		mes "Até logo~";
		close;
	}
}

//============================================================ 
//= Jogo da Maçã
//============================================================ 
airplane_01,33,68,4	script	Clarice	1_F_MERCHANT_02,{
	mes "[Clarice]";
	mes "Oi, eu sou Clarice~";
	mes "Você gostaria de apostar algumas maçãs no amigável Jogo dos Dados?";
	next;
	callfunc "jogodamaçã","Clarice";
	end;
}

function	script	jogodamaçã	{

	switch (select("Jogar:Regras do Jogo:Cancelar")) {
	case 3:
		mes "["+getarg(0)+"]";
		mes "Eu estou pronta para jogar o jogo dos dados quando você quiser.";
		mes "Quando quiser jogar basta me dizer, certo?";
		close;
	case 2:
		mes "["+getarg(0)+"]";
		mes "As regras do Jogo dos Dados são bastante simples.";
		mes "Primeiro, você deve saber que as apostas são Maçãs.";
		mes "Você pode apostar no máximo 50 Maçãs por vez.";
		mes "Para que tudo ocorra bem, eu aceito apenas Maçãs.";
		next;
		mes "["+getarg(0)+"]";
		mes "Mas ei, se você possuir tantos zenys que já não cabem mais em seus bolsos, tire os para fora e você pode comprar quantas maçãs quiser no Vendedor de Maçãs~";
		next;
		mes "["+getarg(0)+"]";
		mes "Agora, nós iremos começar rolando dois dados de 6 lados.";
		mes "Enquanto for seu turno, você poderá rolar dois dados de 6 lados.";
		mes "Após isso, nós teremos a opção de rolar um terceiro dado.";
		next;
		mes "["+getarg(0)+"]";
		mes "Agora aqui é a parte importante.";
		mes "Se ao total nos dados você atingir 12, você irá a falência e irá perder.";
		mes "Caso contrário, a pessoa com a maior pontuação irá vencer.";
		mes "O que acha?";
		next;
		mes "["+getarg(0)+"]";
		mes "Agora, primeiro você deve decidir se quer jogar o terceiro dado ou não.";
		mes "Então, dependendo do seu resultado, eu irei rolar meu terceiro dado...";
		mes "Ou talvez não.";
		next;
		mes "["+getarg(0)+"]";
		mes "Quando você ganhar, você irá receber o dobro de Maçãs que você apostou.";
		mes "Mas se por acaso você empatar, você irá receber de volta apenas as Maçãs que você apostou.";
		mes "Justo não?";
		close;
	case 1:
		break;
	}
	mes "["+getarg(0)+"]";
	mes "Ah, Então você quer jogar comigo?";
	mes "Ótimo! Quantas Maçãs você deseja apostar?";
	mes "Lembre-se, seu limite são 50 Maçãs.";
	mes "Se você deseja cancelar, por favor digite '0'.";
	next;
	while(1) {
		input .@amount;
		if (.@amount == 0) {
			mes "["+getarg(0)+"]";
			mes "Mudou de idéia?";
			mes "Eu entendo. Bem então, espero que possamos jogar depois.";
			close;
		} else if (.@amount < 1 || .@amount > 50) {
			mes "["+getarg(0)+"]";
			mes "Você não pode escolher mais do que 50 ou menos que 1 Maçã.";
			mes "Lembre-se precisamos manter as apostas em valores razoáveis.";
			next;
			continue;
		}
		mes "["+getarg(0)+"]";
		mes "Então você deseja apostar ^FF0000"+.@amount+"^000000 Maçãs.";
		mes "Certo?";
		next;
		if (select("Sim:Não") == 2) {
			mes "["+getarg(0)+"]";
			mes "Hum, cometi um erro?";
			mes "Certo, por favor digite o número de Maçãs que você deseja apostar.";
			next;
			continue;
		}
		if (countitem(512) <.@amount) {
			mes "Me desculpe, mas você não possui essa quantidade de Maçãs que deseja apostar...";
			mes "Você não pode apostar se não tem a aposta, você sabe disso.";
			next;
			continue;
		}
		delitem 512,.@amount;
		mes "["+getarg(0)+"]";
		mes "Bom!";
		mes "Agora nós vamos começar o Jogo!";
		mes "Eu irei rolar primeiro~";
		break;
	}
	mes "^3355FF*Rolando*^000000";
	next;
	.@giveapple = .@amount*2;
	.@table1 = rand(1,6);
	.@table2 = rand(1,6);
	.@tablesub = .@table1 + .@table2;
	.@tabletotal = .@tablesub;
	mes "["+getarg(0)+"]";
	mes "Eu tenho um ^0000FF" + .@table1 + "^000000 e um ^0000FF" + .@table2 + "^000000.";
	mes "Meu total é ^0000FF" + .@tablesub + "^000000.";
	mes "^FF0000" + strcharinfo(0) + "^000000, agora é sua vez.";
	next;
	select("Lançar Dado.");
	mes "^3355FF*Rolando*^000000";
	.@player1 = rand(1,6);
	.@player2 = rand(1,6);
	.@playersub = .@player1 + .@player2;
	if (.@playersub > 9 && .@amount > 39) {
		.@player1 = rand(1,6);
		.@player2 = rand(1,6);
		.@playersub = .@player1 + .@player2;
	}
	.@playertotal = .@playersub;
	next;
	mes "["+getarg(0)+"]";
	mes "^FF0000" + strcharinfo(0) + "^000000, você possui ^FF0000" + .@player1 + "^000000 e ^FF0000" + .@player2 + "^000000. O total é ^FF0000" + .@playersub + "^000000 .";
	next;
	mes "["+getarg(0)+"]";
	if(.@playersub == .@tablesub) {
		mes "Atualmente meu total é ^0000FF" + .@tablesub + "^000000 e ^FF0000" + strcharinfo(0) + "^000000, seu total é ^FF0000" + .@playersub + "^000000. Estamos empatados, gostaria de lançar o dado novamente?";
	} else if (.@playersub > .@tablesub) {
		mes "Atualmente meu total é ^0000FF" + .@tablesub + "^000000 e ^FF0000" + strcharinfo(0) + "^000000, seu total é ^FF0000" + .@playersub + "^000000. ^FF0000" + strcharinfo(0) + "^000000, atualmente você está ganhando o jogo, gostaria de lançar o dado novamente?";
	} else if(.@tablesub > .@playersub) {
		mes "Atualmente meu total é ^0000FF" + .@tablesub + "^000000 e ^FF0000" + strcharinfo(0) + "^000000, seu total é ^FF0000" + .@playersub + "^000000. Eu estou vencendo o jogo. Gostaria de lançar o dado novamente?";
	}
	next;
	switch (select("Lançar Dado.:Cancelar.")) {
	case 1:
		mes "^3355FF*Rolando*^000000";
		.@player3 = rand(1,6);
		.@playertotal += .@player3;
		next;
		mes "["+getarg(0)+"]";
		if (.@playertotal > 12) {
			mes "^FF0000" + strcharinfo(0) + "^000000, você possui  ^FF0000" + .@player3 + "^000000 e o total agora é ^FF0000" + .@playertotal + "^000000. Você perdeu o jogo. Me desculpe, tente novamente depois.";
			close;
		} else if (.@playertotal < .@tablesub) {
			mes "^FF0000" + strcharinfo(0) + "^000000, você possui  ^FF0000" + .@player3 + "^000000 e o total agora é ^FF0000" + .@playertotal + "^000000. Mesmo que você lance o dado novamente, seu total ainda será menor do que o meu. Você perdeu o jogo. Eu sinto muito e espero que joguemos numa próxima.";
			close;
		} else if (.@playertotal == .@tablesub) {
			if (.@tablesub > 8) {
				mes "^FF0000" + strcharinfo(0) + "^000000, você possui  ^FF0000" + .@player3 + "^000000 e o total agora é ^FF0000" + .@playertotal + "^000000. Eu não quero correr nenhum risco, vamos terminar esse jogo em empate. Jogaremos denovo uma outra hora~";
				close2;
				getitem 512,.@amount; // Maça
				end;
			}
		} else {
			mes "^FF0000" + strcharinfo(0) + "^000000, você possui ^FF0000" + .@player3 + "^000000 e o total agora é ^FF0000" + .@playertotal + "^000000. É minha vez de jogar.";
		}
		break;
	case 2:
		mes "["+getarg(0)+"]";
		if (.@playersub > .@tablesub) {
			mes "Entendo, você não quer correr o risco de perder o jogo. Ok, deixe lançar o dado mais uma vez.";
		} else if (.@playersub == .@tablesub) {
			if (.@tablesub > 8) {
				mes "Entendo, você não quer correr o risco de perder o jogo. Nem eu, vamos acabar com esse jogo em um empate. Jogaremos denovo uma outra hora~";
				close2;
				getitem 512,.@amount; // Maçã
				end;
			}
			mes "Certo.";
			mes "Vamos rolar os dados novamente.";
		} else {
			mes "Não faria mal tentar.";
			mes "Bem, eu venci dessa vez.";
			mes "Me desculpe, mas poderemos jogar denovo numa próxima.";
			close;
		}
		break;
	}
	next;
	mes "^3355FF*Rolando*^000000";
	.@table3 = rand(1,6);
	.@tabletotal += .@table3;
	next;
	mes "["+getarg(0)+"]";
	if (.@tabletotal > 12) {
		mes "Eu possuo ^0000FF" + .@table3 + "^000000 e meu total agora é ^0000FF" + .@tabletotal + "^000000. Eu perdi o jogo por que meu total excedeu 12. Irei lhe dar as Maçãs. Parabéns, você jogou muito bem.";
		close2;
		getitem 512,.@giveapple; // Maçã
		end;
	} else if (.@playertotal > .@tabletotal) {
		mes "Eu possuo ^0000FF" + .@table3 + "^000000 e meu total agora é ^0000FF" + .@tabletotal + "^000000. Como você já possui o total de ^FF0000" + .@playertotal + "^000000 você venceu o jogo, ^FF0000" + strcharinfo(0) + "^000000. Irei lhe dar as maçãs. Foi um grande jogo e espero que possamos jogar denovo em uma outra hora.";
		close2;
		getitem 512,.@giveapple; // Maçã
		end;
	} else if (.@playertotal == .@tabletotal) {
		mes "Eu possuo ^0000FF" + .@table3 + "^000000 e meu total agora é ^0000FF" + .@tabletotal + "^000000. Com o total de ^FF0000" + .@playertotal + "^000000 esse jogo acabou em empate, ^FF0000" + strcharinfo(0) + "^000000. Deixe-me te dar suas maçãs de volta. Foi um grande jogo e espero que possamos jogar denovo em uma outra hora.";
		close2;
		getitem 512,.@amount; // Maçã
		end;
	} else if (.@playertotal < .@tabletotal) {
		mes "Eu possuo ^0000FF" + .@table3 + "^000000 e meu total agora é ^0000FF" + .@tabletotal + "^000000. Com o total de ^FF0000" + .@playertotal + "^000000 você perdeu esse jogo, ^FF0000" + strcharinfo(0) + "^000000. Me desculpe, tente novamente depois.";
		close;
	}
}
