//  ________________________________________________________
// /                                                        \
// |       _           _   _   _                            |
// |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
// |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
// |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
// |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
// |                                                        |
// |                    brAthena Script                     |
// |--------------------------------------------------------|
// | Nome do Script: Quest do Arqueiro                      |
// |--------------------------------------------------------|
// | Criado por: eA/brA Dev. Team                           |
// |--------------------------------------------------------|
// | Versões eA/brA: 1.9/1.3                                |
// |--------------------------------------------------------|
// | Descrição: Mudança de Classe para Arqueiro             |
// |--------------------------------------------------------|
// | Changelog:                                             |
// | 1.0 Script Traduzido [Protimus]                        |
// | 1.1 Script Revisado [insidemybrain]                    |
// | 1.2 Corrigido alguns erros [Wolf]                      |
// | 1.3 Otimizações no npc & remoções [Wolf]               |
// \________________________________________________________/

payon_in02,64,71,4	script	Arqueiro#archer	4_M_03,{
	if (Upper == 1) {
		mes "[Membro da Guilda dos Arqueiros]";
		if (Class == Job_Novice_High && (ADVJOB == Job_Sniper || ADVJOB == Job_Clown || ADVJOB == Job_Gypsy)) {
			mes "Você não me é estranho...";
			mes "Você quer não que ser um arqueiro?";
			next;
			mes "[Membro da Guilda dos Arqueiros]";
			if (getskilllv("NV_BASIC") < 9) {
				mes "Como você já sabe, para ser um arqueiro você precisa aprender";
				mes "todas as habilidades básicas.";
				next;
				mes "[Membro da Guilda dos Arqueiros]";
				mes "Certo? Cuide-se.";
			} else {
				mes "Não preciso dizer muita coisa.";
				mes "Basta mirar bem usando os seus sentidos.";
				next;
				skill 143,0,0;
			jobchange Job_Archer_High;
				skill 147,1,0;
				skill 148,1,0;
				mes "[Membro da Guilda dos Arqueiros]";
				mes "Não temos nada aqui neste momento...";
				mes "Bem, isso não importa. Divirta-se.";
			}
			close;
		} else {
			mes "O que você está olhando?";
			mes "Apenas vá embora...";
			mes "Eu sou bem orgulhoso...";
			mes "Ôpa, não, não...!";
			next;
			mes "[Membro da Guilda dos Arqueiros]";
			mes "Haha, você não ouviu nada do que eu disse.";
			mes "Entendeu tudo errado.";
			close;
		}
	}
	mes "[Membro da Guilda dos Arqueiros]";
	mes "Bom dia. Como posso ajudá-lo?";
	next;
	switch(select("Eu quero ser um Arqueiro.:O que é preciso para ser um Arqueiro?:Não preciso de ajuda.")) {
	case 1:
		mes "[Membro da Guilda dos Arqueiros]";
		if (BaseJob == Job_Archer) {
			mes "Você já é um arqueiro.";
			close;
		} else if (BaseJob != Job_Archer && BaseJob != Job_Novice) {
			mes "Hmm... Você não parece ser um Aprendiz...";
			mes "Seja lá o que você for, você não pode ser um arqueiro, porque já é de outra classe.";
			close;
		}
		if (job_archer_q == 0) {
			mes "Você quer ser um arqueiro?";
			mes "Se você quer, preencha este formulário.";
			next;
			if (select("Inscrever-se.:Cancelar.") == 1) {
				job_archer_q = 1;
				setquest 1004;
				mes "[Membro da Guilda dos Arqueiros]";
				mes "Ok, assine aqui... Certo... Hmm, você será promovido de classe assim que cumprir os pré-requisitos necessários.";
				mes "Se você acha que está pronto, podemos começar agora.";
				mes "Você está pronto?";
				next;
				if (select("Sim!:Não, não estou pronto ainda.") == 1) {
					mes "[Membro da Guilda dos Arqueiros]";
					mes "Vamos ver então.";
					next;
				} else {
					mes "[Membro da Guilda dos Arqueiros]";
					mes "Compreendo...";
					mes "Se você quiser saber os pré-requisitos para se tornar um Arqueiro, apenas fale comigo.";
					close;
				}
			}
			mes "[Membro da Guilda dos Arqueiros]";
			mes "Até mais!";
			close;
		}
		mes "[Membro da Guilda dos Arqueiros]";
		mes "Você se chama "+strcharinfo(0)+"?";
		next;
		mes "[Membro da Guilda dos Arqueiros]";
		if (getskilllv("NV_BASIC") < 9) {
			mes "O seu nível como Aprendiz está abaixo do necessário...";
			mes "Você deve chegar ao nível 10 de classe para se tornar um Arqueiro.";
			close;
		}
		if (job_archer_q == 1) {
			.@archer_item1 = countitem(1066) * 5;
			.@archer_item2 = countitem(1067) * 3;
			.@archer_item3 = countitem(1068) * 2;
			.@archer_item4 = countitem(1019);
			.@total_archer = .@archer_item1 + .@archer_item2 + .@archer_item3 + .@archer_item4;
			.@total_archer2 = (((.@archer_item2 + .@archer_item3) * 2) + .@archer_item4);
			mes "Ótimo! Bom trabalho!";
			mes "[Membro da Guilda dos Arqueiros]";
			mes "Agora vamos ver se você trouxe Troncos suficientes para fazermos um Arco.";
			next;
			mes "[Membro da Guilda dos Arqueiros]";
			if (countitem(1066) == 0 && countitem(1067) == 0 && countitem(1068) == 0 && countitem(1019) == 0) {
				mes "...Hmm...";
				mes "Você não me trouxe nada!";
				close;
			}
			if (countitem(1066)) mes "Tronco Tipo 1: "+countitem(1066)+" unidades: "+.@archer_item1+".";
			if (countitem(1067)) mes "Tronco Tipo 2: "+countitem(1067)+" unidades: "+.@archer_item2+".";
			if (countitem(1068)) mes "Tronco Tipo 3: "+countitem(1068)+" unidades: "+.@archer_item3+".";
			if (countitem(1019)) mes "Tronco Tipo 4: : "+countitem(1019)+" unidades: "+.@archer_item4+".";
			if (.@total_archer < 25) {
				mes "O seu total de pontos foi: ^FF0000"+.@total_archer+"^000000/40";
				next;
				mes "[Membro da Guilda dos Arqueiros]";
				mes "Menos de 25?!?! Se esforce um pouco mais...";
				close;
			} else {
				mes "O seu total de pontos foi: ^0000FF"+.@total_archer+"^000000/40";
				next;
				mes "[Membro da Guilda dos Arqueiros]";
				if (.@total_archer > 40) mes "Nossa! Mais de 40! Fantástico! Meus parabéns!";
				else mes ( .@total_archer > 30 ) ? "Mais de 30! Ótimo! Parabéns.":"Passou raspando... Mas tudo bem!";
			}
			next;
			mes "[Membro da Guilda dos Arqueiros]";
			mes "Vou mandar o material para o Departamento de Produção de Arcos.";
			if (countitem(1066)) delitem 1066,countitem(1066); //Tree_Of_Archer_1
			if (countitem(1067)) delitem 1067,countitem(1067); //Tree_Of_Archer_2
			if (countitem(1068)) delitem 1068,countitem(1068); //Tree_Of_Archer_3
			if (countitem(1019)) delitem 1019,countitem(1019); //Wooden_Block
		}
		next;
		callfunc "Job_Change",Job_Archer;
		callfunc "F_ClearJobVar";
		completequest 1004;
		mes "[Membro da Guilda dos Arqueiros]";
		mes "Parabéns, você passou no teste! Vou nomeá-lo um Arqueiro!";
		next;
		mes "[Membro da Guilda dos Arqueiros]";
		mes "Parabéns! Nós esperamos que você contribua para a grandiosidade de nossa Guilda!";
		next;
		mes "[Membro da Guilda dos Arqueiros]";
		mes "Ah! Seu Arco chegou do Departamento de Produção, aqui está ele.";
		getitem 1702,1; //Bow_
		getitem 1750,.@total_archer2; //Arrow
		next;
		mes "[Membro da Guilda dos Arqueiros]";
		mes "Agora você é um Arqueiro legítimo, com arco, flecha e tudo mais!";
		mes "Boa Sorte em sua jornada pelo mundo, espero que você se torne o melhor de todos os Arqueiros!";
		close;
	case 2:
		mes "[Membro da Guilda dos Arqueiros]";
		mes "Eu vou explicar os pré-requisitos para se tornar Arqueiro:";
		if (BaseJob != Job_Novice) {
			next;
			mes "[Membro da Guilda dos Arqueiros]";
			mes ( BaseJob == Job_Archer ) ? ".... mas você já é um Arqueiro...":"Hmm.. você já pertence a uma classe...";
			mes ".... E daí? Quem se importa?";
		}
		next;
		mes "[Membro da Guilda dos Arqueiros]";
		mes "Antes de tudo você deve alcançar nível 10 de classe como Aprendiz.";
		mes "Arqueiros precisam ter muita concentração, devido a dificuldade de se mirar.. Não podemos aceitar pessoas que não se concentram bem.";
		next;
		mes "[Membro da Guilda dos Arqueiros]";
		mes "Você vai precisar juntar quantos ^FF0000Troncos^000000 conseguir.";
		mes "Existem 4 tipos de Troncos, de diferentes qualidades de madeira, logo cada um tem uma pontuação diferente.";
		mes "Você se tornará um Arqueiro se obtiver pelo menos ^0000FF25 ^000000 pontos num total de 40.";
		next;
		mes "[Membro da Guilda dos Arqueiros]";
		mes "Você pode conseguir Troncos dos 'Salgueiros'. Mesmo que não sejam monstros perigosos, tenha cuidado.";
		close;
	case 3:
		close;
	}
}
