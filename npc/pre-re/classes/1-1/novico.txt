//  ________________________________________________________
// /                                                        \
// |       _           _   _   _                            |
// |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
// |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
// |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
// |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
// |                                                        |
// |                 brAthena Script                        |
// |--------------------------------------------------------|
// | Nome do Script: Quest do Noviço                        |
// |--------------------------------------------------------|
// | Criado por: eA/brA Dev. Team                           |
// |--------------------------------------------------------|
// | Versões eA/brA: 2.2/1.3                                |
// |--------------------------------------------------------|
// | Descrição: Mudança de Classe para Noviço               |
// |--------------------------------------------------------|
// | Changelog:                                             |
// | 1.0 Script Traduzido [Protimus]                        |
// | 1.1 Script Revisado [insidemybrain]                    |
// | 1.2 Remoção de alguns espaços [Wolf]                   |
// | 1.3 Otimizações no npc [Wolf]                          |
// \________________________________________________________/

prt_church,184,41,4	script	Necrus#aco	1_M_PASTOR,{
	if (Upper == 1) {
		if (ADVJOB == Job_High_Priest || ADVJOB == Job_Champion) {
			mes "[Necrus]";
			if (Class == Job_Novice_High) {
				mes "Depois de muito tempo, você voltou a este lugar.";
				mes "Você deve ter ouvido, visto e experimentado muitas coisas.";
				next;
				mes "[Necrus]";
				if (getskilllv("NV_BASIC") < 9) {
					mes "Posso torná-lo um Noviço, mas será necessário reaprender os fundamentos.";
					next;
					mes "[Necrus]";
					mes "Ok... Esperarei aqui por você.";
					mes "Cuide-se.";
				} else {
					mes "Você volta a trilhar a estrada do clero,";
					mes "toda a igreja de Prontera dá as boas-vindas a você";
					next;
					skill 143,0,0;
					jobchange Job_Acolyte_High;
					skill 156,1,0;
					mes "[Necrus]";
					mes "Mais uma vez, você pode progredir.";
					mes "Abençôo o seu futuro com a esperança.";
				}
				close;
			} else {
				mes "Mais uma vez, você pode progredir.";
				mes "Abençôo o seu futuro com a esperança.";
				close;
			}
		} else {
			mes "[Necrus]";
			mes "Você renasceu depois de muito tempo.";
			mes "Você deve ter ouvido, visto e experimentado muitas coisas.";
			next;
			mes "[Necrus]";
			mes "Entretanto, suas experiências de vida não condizem com o caminho de um Noviço.";
			mes "Recorde-se de suas memórias e descubra qual o seu verdadeiro caminho.";
			close;
		}
	}
	mes "[Necrus]";
	mes "O que você procura na casa do Senhor?";
	next;
	switch(select("Eu desejo me tornar um Noviço:Como me torno um noviço?:Eu vim olhar os vitrais")) {
	case 1:
		mes "[Necrus]";
		if (BaseJob == Job_Acolyte) {
			mes "Mas meu filho, você já é um Noviço.";
			close;
		} else if (BaseJob != Job_Novice) {
			mes "Vejo que você já tem uma classe definida, desejo sucesso a você!";
			close;
		}
		if (job_acolyte_q == 0) {
			mes "Você deseja mesmo, de todo o seu coração, se tornar um servo do Senhor?";
			next;
			if (select("Sim.:Não, eu menti.") == 1) {
				mes "[Necrus]";
				mes "Muito bom. Eu aceito a vontade de "+strcharinfo(0)+" em se tornar um Noviço.";
				next;
				mes "[Necrus]";
				mes "Espero que você entenda que para se tornar um servo do Senhor, deverás realizar uma penitência.";
				next;
				switch(rand(3)) {
				mes "A sua penitência é uma forma de peregrinação.";
				default:
					job_acolyte_q = 2;
					mes "Você deve visitar o ^000077 Irmão Simão ^000000, ele se isolou com o animais perto da ^000077Abadia ao Nordeste de Prontera^000000.";
					setquest 1001;
					break;
				case 1:
					job_acolyte_q = 3;
					mes "Você deve visitar a ^000077 Irmã Matilda ^000000, ela se isolou no deserto, ^000077no mapa ao Norte de Morroc^000000.";
					setquest 1002;
					break;
				case 2:
					job_acolyte_q = 4;
					mes "Você deve visitar o ^000077 Irmão Bartolomeu ^000000, ele pode ser encontrado ^000077em uma ponte à Noroeste de Prontera^000000.";
					setquest 1003;
					break;
				}
				next;
				mes "[Necrus]";
				mes "Que a graça do Senhor o acompanhe em sua jornada!";
				close;
			}
			mes "[Necrus]";
			mes "Que a vontade do Senhor seja a sua segurança.";
			close;
		}
		mes "Oh, você voltou!";
		next;
		mes "[Necrus]";
		if (getskilllv("NV_BASIC") < 9) {
			mes "Meu bom Deus! Você ainda não tem nível 9 de habilidade básicas, não é possivel servir ao Senhor assim.";
			close;
		}
		if (job_acolyte_q < 5) {
			mes "Deixe-me dizer qual será sua peregrinação.";
			next;
			switch(job_acolyte_q) {
			case 2:
				mes "[Necrus]";
				mes "Você deve visitar o ^000077 Irmão Simão ^000000, ele se isolou com o animais perto da ^000077Abadia ao Nordeste de Prontera^000000.";
				break;
			case 3:
				mes "[Necrus]";
				mes "Você deve visitar a ^000077 Irmã Matilda ^000000, ela se isolou no deserto, ^000077no mapa ao Norte de Morroc^000000.";
				break;
			case 4:
				mes "[Necrus]";
				mes "Você deve visitar o ^000077 Irmão Bartolomeu ^000000, ele pode ser encontrado ^000077em uma ponte à Noroeste de Prontera^000000.";
				break;
			}
			next;
			mes "[Necrus]";
			mes "Complete sua penitência antes de retornar.";
			close;
		}
		mes "Você completou sua peregrinação e isso enche meu coração de alegria. Neste momento o recebo nos caminhos do Senhor e o torno um Noviço.";
		next;
		skill 143,0,0;
		callfunc "Job_Change",Job_Acolyte;
		callfunc "F_ClearJobVar";
		if (questprogress(1001)) {
			completequest 1001;
		}
		else if (questprogress(1002)) {
			completequest 1002;
		}
		else {
			completequest 1003;
		}
		mes "[Necrus]";
		mes "Tenha sempre Deus em seu coração, seja nas horas de tranquilidade ou no calor da batalha, nunca se esqueça Dele, que nos ama tanto apesar de sermos imperfeitos.";
		next;
		mes "[Necrus]";
		mes "Do mesmo modo que você não hesitou em sua penitência, eu espero que você nunca hesite frente às tentações do pecado.";
		close;
	case 2:
		mes "[Necrus]";
		mes "Os pré-requisitos para se tornar um Noviço são dois.";
		mes "Por favor, ouça-os com atenção.";
		next;
		mes "[Necrus]";
		mes "Antes de tudo, você deve alcançar Nível 9 de Classe como Aprendiz.";
		mes "Com esse nível, será possível que você realize uma peregrinação em nome do Senhor.";
		next;
		mes "[Necrus]";
		mes "O seu objetivo será o seguinte:";
		if (job_acolyte_q != 0) {
			switch(job_acolyte_q) {
			case 2:
				mes "[Necrus]";
				mes "Você deve visitar o ^000077 Irmão Simão ^000000, ele se isolou com o animais perto da ^000077Abadia ao Nordeste de Prontera^000000.";
				break;
			case 3:
				mes "[Necrus]";
				mes "Você deve visitar a ^000077 Irmã Matilda ^000000, ela se isolou no deserto, ^000077no mapa ao Norte de Morroc^000000..";
				break;
			default:
				mes "[Necrus]";
				mes "Você deve visitar o ^000077 Irmão Bartolomeu ^000000, ele pode ser encontrado ^000077em uma ponte à Noroeste de Prontera^000000.";
				break;
			}
			next;
			mes "[Necrus]";
			mes "Que a graça do Senhor o acompanhe em sua jornada!";
		} else {
			mes "Que a vontade do Senhor seja a sua segurança.";
		}
		next;
		mes "[Necrus]";
		mes "Após completar estes dois requerimentos, será permitido que você se torne um servo de Deus.";
		close;
	case 3:
		close;
	}
}

prt_fild03,365,255,2	script	Irmão Simão#aco	4_M_ORIENT02,{
	mes "[Irmão Simão]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 6) {
			mes "Não se preocupe, já enviei um pombo à Prontera para avisá-lo de sua visita.";
			next;
			mes "[Irmão Simão]";
			mes "Agora se preocupe apenas em como voltar em segurança para a cidade.";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 2) {
				mes "Oh... Você é o jovem que deseja se tornar um Noviço, não é?";
				next;
				mes "Você se chama "+strcharinfo(0)+", certo? Bem, muito obrigado por ter vindo me visitar!";
				mes "Eu acredito que Necrus já lhe contou sobre os noviços.";
				next;
				mes "[Irmão Simão]";
				mes "Eu espero que a sua Fé nunca falhe nos anos de provação que estão por vir.";
				next;
				mes "[Irmão Simão]";
				mes "Entretanto, as lições que você veio aprender aqui eram as que você já esperava.";
				next;
				mes "[Irmão Simão]";
				mes "Eu mandarei a notícia de que você chegou até aqui para o Necrus.";
				mes "Desejo-lhe uma jornada segura de volta à Prontera.";
				next;
				mes "[Irmão Simão]";
				mes "Adeus e que o Senhor esteja contigo.";
				close2;
				savepoint "prt_fild03",361,255;
				job_acolyte_q = 6;
				end;
			} else {
				mes "Oh... Você é o jovem que deseja se tornar um Noviço, não é?";
				next;
				mes "[Irmão Simão]";
				mes "Você se chama "+strcharinfo(0)+", certo? Hmm.. Sinto informa-lhe que seu nome não se encontra em minha lista.";
				next;
				mes "[Irmão Simão]";
				mes "Recomendo que você verifique corretamente qual é a sua peregrinação.";
				close;
			}
		} else {
			mes "O que lhe traz aqui? Este lugar tem monstros fortes demais para alguém como você.";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_FatherRub";
	else {
		if (BaseJob == Job_Priest) {
			mes "Saudações.";
			next;
			mes "[Irmão Simão]";
			mes "Seja bem vindo, sinta-se a vontade para sentar e contemplar a palavra do Senhor comigo.";
			mes "Este lugar é lindo, mesmo quando o perigo nos acompanha, podemos sentir a serenidade dele...";
			close;
		} else {
			mes "Você veio aqui para treinar? ou é somente um forasteiro?";
			next;
			mes "[Irmão Simão]";
			mes "Independente de quem você seja, tome cuidado. Os monstros aqui são incrívelmente fortes, apesar de sua aparência bonitinha.";
			close;
		}
	}
}

moc_fild07,41,355,4	script	Irmã Matilda#2aco	4_F_SISTER,{
	mes "[Irmã Matilda]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 7) {
			mes "Não se preocupe, já enviei um pombo à Prontera para avisá-lo de sua visita.";
			mes "Agora se preocupe apenas em como voltar em segurança para a cidade.";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 3) {
				mes "Oh... Você é o jovem que deseja se tornar um Noviço, não é?";
				next;
				mes "[Irmã Matilda]";
				mes "Você se chama "+strcharinfo(0)+", certo? Bem, muito obrigada por ter vindo me visitar!";
				mes "Eu mandarei a notícia de que você chegou até aqui para o Necrus.";
				mes "Desejo-lhe uma jornada segura de volta à Prontera.";
				close2;
				savepoint "moc_fild07",35,355;
				job_acolyte_q = 7;
				end;
			} else {
				mes "[Irmã Matilda]";
				mes "Oh... Você é o jovem que deseja se tornar um Noviço, não é?";
				mes "Você se chama "+strcharinfo(0)+", certo? Hmm...";
				mes "Sinto informa-lhe que seu nome não se encontra em minha lista.";
				next;
				mes "[Irmã Matilda]";
				mes "Recomendo que você verifique corretamente qual é a sua peregrinação.";
				close;
			}
		} else {
			mes "...........";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_MotherMart";
	else {
		if (BaseJob == Job_Priest) {
			mes "Olá!";
			next;
			mes "[Irmã Matilda]";
			mes "Como você vem praticado a graça do Senhor depois de tanto tempo? Eu espero que você esteja gostando de viver a graça do Senhor.";
			close;
		} else {
			mes "Que Deus";
			mes "esteja com você...";
			close;
		}
	}
}

prt_fild00,208,218,6	script	Irmão Bartolomeu#3aco	4W_M_02,{
	mes "[Irmão Bartolomeu]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 8) {
			mes "Eu acho melhor você começar o seu caminho de volta...";
			mes "Ou você quer ficar aqui comigo vendo a grama crescer!?";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 4) {
				mes "Ou você é um Aprendiz querendo ser Noviço ou eu sou um Caramelo!";
				next;
				mes "[Irmão Bartolomeu]";
	   			mes "Nada mal hein, chegou até aqui inteiro...";
	  			mes "O seu nome é "+strcharinfo(0)+", não é?";
	   			next;
	   			mes "[Irmão Bartolomeu]";
	   			mes "Então está tudo certinho, vou avisar o Necrus de que você chegou até aqui.";
	   			mes "Agora volte para Prontera e fale com ele.";
				close2;
				savepoint "prt_fild00",206,230;
				job_acolyte_q = 8;
				end;
			} else {
				mes "Ei, você aí!";
				mes "Ou você é um Aprendiz querendo ser Noviço ou eu sou um Caramelo!";
				next;
				mes "[Irmão Bartolomeu]";
				mes "Nada mal hein, chegou até aqui inteiro...";
				mes "Qual o seu nome? "+strcharinfo(0)+"? Hmmm... O seu nome não está na minha lista...";
				next;
				mes "[Irmão Bartolomeu]";
				mes "Acho que você se confundiu, você deve visitar outra pessoa, não eu.";
				mes "Volte para Prontera e consulte o Necrus novamente.";
				close;
			}
		} else {
			mes "Não, eu não sou o Bicho-Papão da ponte...";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_FatherYos";
	else {
		if (BaseJob == Job_Priest) {
			mes "Ei...";
			next;
			mes "[Irmão Bartolomeu]";
			mes "Se quiser, venha sentar-se e meditar grandes verdades. A grandeza de Deus é magnífica...";
			close;
		} else {
			mes "Você tem algo para me dizer? Por que infelizmente para você,";
			mes "eu não tenho nada.";
			close;
		}
	}
}
