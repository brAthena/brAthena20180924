//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: Quests Niffheim                        |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.9/1.6/1.2                         |
//   |--------------------------------------------------------|
//   | Descrição: Quest Teclas do Piano, Mensagem de Entrega  |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Cabeçalho Criado [Holy]                            |
//   | 1.1 Tradução PT-BR [Holy]                              |
//   | 1.2 Suporte ao registro de quests [lututuiBR]          |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

// Quest Teclas do Piano
//============================================================
niflheim,224,243,3	script	Alreg#nif	4_M_NFDEADMAN,{
	if (nif_piano) set nif_piano,0;
	mes "[Alreg]";
	mes "Eu gosto de carne, haha.";
	mes "Você!? *Olhar tentador*.";
	next;
	mes "[Alreg]";
	mes "Eu quero saborear você!";
	next;
	mes "^FF3355*Crunch* Crunch*^000000";
	if (nif_esc == 0 && (MISC_QUEST & 32) == 0) {
		set piano_root,rand(1,2);
		percentheal -60,0;
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "NãoOoOOOoOoo~!";
		next;
		mes "[Alreg]";
		mes "Faz muito tempo desde que não via carne,";
		mes "então... Seu sabor";
		mes "é bom. Tome isso,";
		mes "é um presente!";
		mes "*Burrrp*";
		nif_esc = rand(1,2);
		getitem 7184,1; //Tecla de Piano
		close;
	}
	percentheal -30,0;
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "Ow~! O que você está";
	mes "tentando fazer, me matar?";
	next;
	mes "[Alreg]";
	mes "Hum? Não, não não não.";
	mes "Eu estou tentando te comer!";
	close;
}

nif_in,105,81,3	script	Crayu#nif	4_M_NFDEADMAN2,{
	mes "[Crayu]";
	mes "Viva...";
	mes "Escute esse som...";
	mes "Vem sendo cantado por muitos por um";
	mes "bom tempo, mas ninguém sabe";
	mes "quando foi feito ou quem escreveu.";
	next;
	mes "[Crayu]";
	mes "^FF00001. Montanha-Sol para o oeste";
	mes "^FF00002. Onde o crepúsculo é roxo ";
	mes "^FF00003. Rodeado por uma bela melodia";
	mes "^FF00004. Você se torna a chave que ignora o capitão^000000";
	next;
	if (nif_esc == 1 || nif_esc == 4) {
		mes "[Crayu]";
		mes "Certo, espere! Aqui está um teste para você!";
		switch(rand(1,4)) {
		case 1:
			.@songline$ = "Montanha-Sol para o oeste";
			mes "Anote a primeira linha da canção.";
			break;
		case 2:
			.@songline$ = "Onde o crepúsculo é roxo";
			mes "Anote a segunda linha da canção.";
			break;
		case 3:
			.@songline$ = "Rodeado por uma bela melodia";
			mes "Anote a terceira linha da canção.";
			break;
		case 4:
			.@songline$ = "Você se torna a chave que ignora o capitão";
			mes "Anote a quarta linha da canção.";
			break;
		}
		next;
		input .@input$;
		if (.@input$ == .@songline$) {
			mes "[Crayu]";
			mes "Excelente...!";
			mes "Então você está escutando~!";
			mes "Isso faz de você uma das minhas";
			mes "pesoas favoritas. Aqui está um";
			mes "pequeno prêmio para você.";
			next;
			if (nif_esc == 1) nif_esc = 3;
			else nif_esc = 6;
			getitem 7184,1; //Piano_Keyboard
			mes "[Crayu]";
			mes "Por último, espero que você mostre seu";
			mes "respeito pelos os outros Bardos, tal como";
			mes "você fez comigo.";
			close;
		}
		else {
			mes "[Crayu]";
			mes "*Suspiro*... Se você só tivesse";
			mes "ouvidos para mim, isso seria";
			mes "fácil de responder.";
			mes "Tente lembrar a linha";
			mes "e tente novamente!";
			close;
		}
	}
	next;
	mes "[Crayu]";
	mes "Eu gostaria de saber o que as letras significam...";
	mes "Elas parecem muito profundas...";
	mes "Tenho a sensação de que está canção";
	mes "carrega algum segredo...";
	close;
}

nif_in,31,20,3	script	Kuzkahina#nif	4_M_NFDEADMAN2,{
	if (nif_esc == 2 || nif_esc == 3) {
		mes "[Kuzkahina]";
		mes "Eu não entendo";
		mes "porque eu não posso ganhar";
		mes "dinheiro com essa loja...";
		mes "Mesmo enquando eu estava vivo";
		mes "eu nunca pude fazer";
		mes "dinheiro com meu trabalho...";
		next;
		mes "[Kuzkahina]";
		mes "Ei, você! Sinta-se útil";
		mes "e jogue isso fora quando sair.";
		nif_esc += 2;
		getitem 7184,1; //Piano_Keyboard
		next;
		mes "[Kuzkahina]";
		mes "Caramba, O que há com esta loja?";
		mes "Como é que eu acabei contratando esses trabalhadores inúteis...?";
		mes "*Grr Grr*";
		mes "*Grr Grr*";
		close;
	}
	mes "[Kuzkahina]";
	mes "Eu não entendo";
	mes "porque eu não posso ganhar";
	mes "dinheiro com essa loja...";
	mes "Mesmo enquando eu estava vivo";
	mes "eu nunca pude fazer";
	mes "dinheiro com meu trabalho...";
	close;
}

niflheim,208,103,1	script	#Graveyard1	HIDDEN_NPC,1,1,{
	end;

OnTouch:
	if (nif_esc == 5 || nif_esc == 7 || nif_esc == 8 || nif_esc == 10) {
		mes "^3355FFVocê encontrou algo";
		mes "meio-enterrado perto de um túmulo.";
		mes "Isso parece ter sido muito bem feito.^000000";
		next;
		mes "^3355FFVocê gostaria de pegar?^000000";
		if (select("Sim:Não") == 1) {
			if (nif_esc < 10)
				nif_esc += 2;
			else if (nif_esc == 10)
				nif_esc = 11;
			getitem 7184,1; //Piano_Keyboard
		}
		close;
	}
	end;
}

niflheim,169,71,1	script	#Graveyard2	HIDDEN_NPC,1,1,{
	end;

OnTouch:
	if (nif_esc == 6 || nif_esc == 9) {
		mes "^3355FFVocê encontrou algo";
		mes "meio-enterrado perto de um túmulo.";
		mes "Isso parece ter sido muito bem feito.^000000";
		next;
		mes "^3355FFVocê gostaria de pegar?^000000";
		if (select("Sim:Não") == 1) {
			nif_esc += 2;
			getitem 7184,1; //Piano_Keyboard
		}
		close;
	}
	end;
}

nif_in,115,181,0	script	#Piano	HIDDEN_NPC,1,1,{
	end;

OnTouch:
	if (nif_esc == 0 && (MISC_QUEST & 32) == 0) {
		mes "^3355FFVocê vê um grande e pesado piano.";
		mes "Você tem a sensação de que";
		mes "sua música soaria magnífica.^000000";
		next;
		mes "^3355FFAo examinar o piano,";
		mes "você encontra 7 teclas";
		mes "que estão faltando";
		mes "no meio do piano.";
		mes "Se você tivesse todas as teclas,";
		mes "você poderia tocar uma música...^000000";
		close;
	}
	if (nif_esc == 11) {
		nif_esc = 12;
		delitem 7184,6; //Tecla de Piano
		mes "^3355FFVocê pegou as teclas que encontrou";
		mes "e inseriu dentro";
		mes "de cada espaço vazio.^000000";
		next;
		mes "^3355FFMas aí você percebe";
		mes "que você está faltando a última";
		mes "tecla que fica no espaço do canto";
		mes "esquerdo do teclado...^000000";
		close;
	}
	end;
}

nif_in,114,181,0	script	#Piano3	HIDDEN_NPC,1,1,{
	end;

OnTouch:
	if (nif_esc == 12 || nif_esc == 13 || MISC_QUEST & 32) {
		mes "^3355FFVocê olha para";
		mes "o espaço vazio da última tecla";
		mes "e mais atento olha novamente...^000000";
		next;
		soundeffect "complete.wav",0;
		next;
		if (nif_esc == 12) {
			MISC_QUEST |= 32;
			nif_piano = 0;
		}
		mes "^3355FFVocê começa a sentir tonturas e";
		mes "seu corpo se sente como se estivese";
		mes "flutuando no ar. Então, sua";
		mes "visão começa a embaçar...^000000";
		close2;
		warp "nif_in",179,163;
		end;
	}
	end;
}

nif_in,188,168,3	script	Witch#nif	4_F_NFDEADMGCIAN,{
	mes "[Kirkena]";
	mes "Huh? ...Você está vivo, não está?";
	mes "Não sei como você foi";
	mes "capaz de chegar aqui sem eu";
	mes "ter conhecimento disso,";
	mes "mas esse lugar é perigoso para";
	mes "se viver.";
	next;
	mes "[Kirkena]";
	mes "Estou te enviando de volta";
	mes "para fora dessa cidade.";
	mes "Se você quer ajudar,";
	mes "é melhor não voltar a";
	mes "esse lugar.";
	close2;
	warp "umbala",138,208;
	end;
}

// Quest Entrega de Mensagem
//=============================================================
prt_in,161,20,3	script	Erious#nif	4_F_03,{
	if (MISC_QUEST & 16384) {
		mes "[Erious]";
		mes "Desejo-lhe muita segurança";
		mes "em suas viagens, aventureiro.";
		close;
	}
	if (nif_revive == 0) {
		mes "[Erious]";
		mes "*Choro Choro*";
		mes "*Choro* Ahhh...!";
		mes "Elly, como você pôde";
		mes "me deixar assim...";
		emotion e_sob;
		next;
		if (select("Confortá-lo.:Ignorá-lo.") == 1) {
			mes "[Erious]";
			mes "Ah, obrigado por sua gentileza.";
			mes "*Choro* Minha esposa morreu enquanto eu estava em";
			mes "uma viagem. Eu sinto muito que";
			mes "não pude estar com ela quando";
			mes "o fim chegou...";
			next;
			mes "[Erious]";
			mes "Eu gostaria de poder falar com a minha Elly,";
			mes "embora seja muito tarde agora.";
			mes "*Choro* Mas...tenho uma última";
			mes "esperança, minha última";
			mes "chance...";
			next;
			mes "[Erious]";
			mes "Foi-me dito que em algum lugar";
			mes "neste mundo, existe uma cidade";
			mes "onde os mortos residem. Eu poderia ser";
			mes "capaz de encontrar minha esposa lá, mas";
			mes "não posso deixar meus filhos aqui";
			mes "sem mim. *Choro*";
			next;
			if (select("Posso entregar a sua mensagem para ela.:Estou tão triste de ouvir isso.") == 1) {
				mes "[Erious]";
				mes "Nossa, você pode?";
				mes "Muito obrigado por sua";
				mes "generosidade. Se você puder fazer";
				mes "isso por mim, eu serei";
				mes "muito grato a você.";
				next;
				mes "[Erious]";
				mes "Você é um aventureiro, não é?";
				mes "Se suas viagens levá-lo para a";
				mes "cidade dos mortos, e se você";
				mes "encontra-la por acaso...";
				next;
				mes "[Erious]";
				mes "Por favor, traga uma mensagem de Elly para mim,";
				mes "deixe que ela saiba que o que aconteceu";
				mes "há um mês, foi tudo minha culpa.";
				mes "Obrigado por sua ajuda,";
				mes "e por favor se cuide.";
				nif_revive = 1;
				setquest 11038;
				close;
			}
			mes "[Erious]";
			mes "*Choro* Não há nenhuma maneira que eu";
			mes "possa vê-la de novo? Se eu pudesse";
			mes "falar com ela pelo menos uma vez mais,";
			mes "mesmo que seja a última vez,";
			mes "eu faria qualquer coisa...";
			close;
		}
		mes "[Erious]";
		mes "*Choros* Eeeeelllllyyyy~~~~!";
		mes "Eu nunca pensei que seria";
		mes "a última vez que a veria";
		mes "meu amor...";
		close;
	}
	else if (nif_revive == 1) {
		mes "[Erious]";
		mes "Me falaram sobre este lugar...";
		mes "A cidade da morte...";
		mes "Pelo que me lembro, ela é";
		mes "localizada perto de algumas áreas de descanso.";
		next;
		mes "[Erious]";
		mes "Todo o dia e noite, ela está cheia";
		mes "de gritos de agonia";
		mes "e desespero. Espero que";
		mes "isso seja útil em sua busca.";
		close;
	}
	else if (nif_revive == 5 || nif_revive == 6) {
		mes "[Erious]";
		mes "Impressionante! Você realmente encontrou a cidade,";
		mes "não foi? Realmente incrível.";
		mes "você já enfrentou todas as dificuldades";
		mes "por mim... Hum... Então, afinal...";
		mes "Você foi capaz de falar com Elly?";
		next;
		if (select("Sim.:Desculpe, eu não posso.") == 1) {
			if (nif_revive == 6 && countitem(934) > 0) {
				mes "[Erious]";
				mes "Você está falando sério? O que ela disse?";
				mes "Ela disse que vai me perdoar?";
				next;
				select("Sim. E ela me deu...isso.");
				delitem 934,1; //Mementos
				mes "[Erious]";
				mes "Ah ... Nesta caixa estão os presentes";
				mes "que lhe dei quando começamos";
				mes "a nos conhecer. Ela tinha uma";
				mes "doença grave, antes mesmo de";
				mes "descobrirmos o que era eu a prometi que";
				mes "iria curar sua doença...";
				next;
				mes "[Erious]";
				mes "É por isso que eu fui na viagem...";
				mes "para encontrar uma cura ou remédio";
				mes "para ela... Mas meus esforços não";
				mes "serviram para nada.";
				mes "...................";
				mes ".........";
				next;
				mes "[Erious]";
				mes "De qualquer forma, agradeço a sua";
				mes "generosidade. Eu quero pagar";
				mes "a você por todos os problemas";
				mes "que te causei, mas eu só tenho";
				mes "o remédio que eu encontrei para ela";
				mes "durante minha viagem.";
				next;
				mes "[Erious]";
				mes "Espero que você tome esse medicamento";
				mes "quando precisar como um sinal da minha gratidão.";
				next;
				if (checkweight(610,26)) {
					MISC_QUEST |= 16384;
					nif_revive = 0;
					completequest 11043;
					getitem 607,2; //Fruto de Yggdrasil
					getitem 608,2; //Sementes de Yggdrasil
					getitem 610,10; //Folha de Yggdrasil
					mes "[Erious]";
					mes "Obrigado por tudo.";
					mes "Desejo-lhe sorte e segurança";
					mes "em suas aventuras.";
					close;
				}
				mes "[Erious]";
				mes "Eu tenho algo para lhe dar,";
				mes "mas você está carregando muitos";
				mes "itens com você no momento.";
				mes "Por que você não";
				mes "guarda alguns de seus itens?";
				close;
			}
			else if (nif_revive == 6 && countitem(934) < 1) {
				mes "[Erious]";
				mes "Hmm...isto é estranho. Tenho certeza que ela lhe daria algo como um sinal de conhecê-la. Por acaso, existe alguma coisa que você não se lembra de me trazer?";
				close;
			}
			mes "[Erious]";
			mes "Acho que você entendeu mal";
			mes "ou você está achando meus sentimentos";
			mes "uma farsa de homem em luto.";
			close;
		}
		if (nif_revive == 5) {
			mes "[Erious]";
			mes "Eu vejo ... você não conseguiu encontrar";
			mes "ela ... No entanto, eu também entendo";
			mes "que cumprir o meu pedido pode";
			mes "ser impossível...";
			next;
			mes "[Erious]";
			mes "Você realmente saiu do seu caminho";
			mes "para me ajudar, então eu quero dar-lhe";
			mes "algo como um sinal da minha";
			mes "gratidão.";
			next;
			if (checkweight(610,10)) {
				MISC_QUEST |= 16384;
				nif_revive = 0;
				completequest 11042;
				getitem 610,10; //Folha de Yggdrasil
				mes "[Erious]";
				mes "Obrigado por tudo.";
				mes "Desejo-lhe sorte e segurança";
				mes "em suas viagens.";
				close;
			}
			mes "[Erious]";
			mes "Eu tenho algo para lhe dar,";
			mes "mas você está carregando muitas";
			mes "coisas por enquanto.";
			mes "Por que você não";
			mes "guarda alguns de seus itens?";
			close;
		}
		mes "[Erious]";
		mes "Eu vejo...";
		mes "...Espere! Não tem nada que pertença";
		mes "à minha esposa, Elly? Por que você está";
		mes "tentando enganar um homem de luto...?";
		close;
	}
	end;
}

umbala,215,181,3	script	Billik	4_M_JOB_KNIGHT1,{
	if (nif_revive > 1 || MISC_QUEST & 16384) {
		mes "[Billik]";
		mes "Bem, faz muito tempo que não te vejo.";
		mes "Se você tiver uma chance no";
		mes "futuro, vamos nos encontrar na";
		mes "cidade da morte.";
		mes "Hahahaha~";
		emotion e_no1;
		close;
	}
	if (nif_revive == 1) {
		mes "[Billik]";
		mes "A cidade dos mortos...";
		mes "É um lugar muito perigoso.";
		mes "Por que você quer ir para lá?";
		next;
		if (select("Eu tenho que encontrar alguém lá.:É uma parte da minha aventura.") == 1) {
			mes "[Billik]";
			mes "Você tem que encontrar alguém lá...";
			mes "Hum... Eu suponho que você deseje";
			mes "contato com os mortos...";
			mes "Eu também posso ver que você está";
			mes "determinado com isso.";
			next;
			mes "[Billik]";
			mes "*Suspiro* Ok, se você realmente quer";
			mes "ir, você deve encontrar um homem chamado";
			mes "'Feylin.' É muito importante";
			mes "que ele saiba que eu apresentei";
			mes "ele a você.";
			next;
			mes "[Billik]";
			mes "Ele não é muito de confiança, mas ele";
			mes "gosta muito de rosas, por isso leve uma rosa";
			mes "para ele. Dessa forma, ele irá saber que";
			mes "eu o enviei.";
			nif_revive = 2;
			changequest 11038,11039;
			next;
			mes "[Billik]";
			mes "A cidade dos mortos está localizada ao";
			mes "norte desse lugar. Lembre-se, é";
			mes "muito perigoso ir até lá,";
			mes "mesmo para um aventureiro";
			mes "experiente, então...";
			mes "tenha cuidado.";
			close;
		}
		mes "[Billik]";
		mes "Hahaha~ Eu acho que você gosta de aventura";
		mes "tanto quanto eu. Embora seja um";
		mes "lugar muito perigoso, você será";
		mes "recompensado na volta pelo seu esforço,";
		mes "mas tenha cuidado.";
		close;
	}
	else if (nif_revive == 0) {
		mes "[Billik]";
		mes "O que te traz aqui? Estou ocupado,";
		mes "saia se você não tiver nada a tratar.";
		close;
	}
}

niflheim,153,215,3	script	Feylin	4_M_NFDEADMAN2,{
	if (nif_revive == 2) {
		mes "[Feylin]";
		if (Sex) {
			mes "Pobre rapaz...";
		} else {
			mes "Pobre garota...";
		}
		mes "Morrer em uma idade tão jovem...";
		mes "É triste ver os";
		mes "jovens morrendo.";
		next;
		mes "[Feylin]";
		mes "............??";
		mes "Ah, eu vejo que você não está morto ainda,";
		mes "está? É inacreditavel";
		mes "ver outra pessoa viva";
		mes "nesta cidade.";
		next;
		mes "[Feylin]";
		mes "Você vem para resgatar alguém,";
		mes "ou você tem outra finalidade?";
		mes "Se você veio aqui para encontrar um";
		mes "falecido amigo seu, você veio";
		mes "falar com a pessoa errada.";
		next;
		mes "[Feylin]";
		mes "Ninguém no Niflheim pode";
		mes "realizar esse tipo de milagre...";
		next;
		mes "[Feylin]";
		mes "Eu posso dizer que você já passou";
		mes "muito sofrimento, mas infelizmente,";
		mes "você veio aqui para nada. Espero";
		mes "você volte de onde veio";
		mes "de antes que seja tarde demais.";
		nif_revive = 3;
		changequest 11039,11040;
		close;
	}
	else if (nif_revive == 3) {
		mes "[Feylin]";
		mes "Eu lhe pedi para sair desta";
		mes "cidade imediatamente. Sinto muito";
		mes "mas eu não sei nada sobre o que";
		mes "você está me perguntando.";
		emotion e_an;
		next;
		if (select("Mas...:Me desculpe.") == 1) {
			mes "[Feylin]";
			mes "Você deve saber de algo...";
			mes "Caso contrário, não seria";
			mes "tão teimoso. Eu não tenho certeza de";
			mes "quem te disse que entrar em contato com o";
			mes "mortos poderia ser possível...";
			next;
			mes "[Feylin]";
			mes "Mas eu certamente não teria dito isso";
			mes "a você. Além disso, falar sobre";
			mes "tais coisas é tabu.";
			nif_revive = 4;
			changequest 11040,11041;
			close;
		}
		mes "[Feylin]";
		mes "Por favor, considere deixar esse";
		mes "lugar o mais rapidamente possível.";
		mes "Ninguém deve ficar em";
		mes "Niflheim por muito tempo.";
		close;
	}
	else if (nif_revive == 4) {
		if (countitem(748) > 0) {
			mes "[Feylin]";
			mes "Huh, Billik deve ter enviado você.";
			mes "Como estou em dívida com ele, eu";
			mes "suponho que irá ajudá-lo.";
			mes "Mas deixe-me avisá-lo:";
			mes "O preço que eu exijo pode ser";
			mes "maior do que o esperado...";
			next;
			if (select("Eu estou disposto a fazer isso.:Eu não acho que eu possa fazer isso.") == 1) {
				if (BaseLevel > 79) {
					mes "[Feylin]";
					mes "Tudo bem, mas você tem que prometer-me";
					mes "que você não vai deixar outras";
					mes "pessoas saberem o que eu estou";
					mes "para lhe dizer.";
					next;
					mes "[Feylin]";
					mes "Esta magia requer um grande";
					mes "sacrifício. Não é preciso dizer, há";
					mes "uma chance dessa magia";
					mes "falhar. Eu também preciso de";
					mes "10 Folhas de Yggdrasil para conjurar essa";
					mes "magia. Você deseja continuar?";
					next;
					if (select("Sim:Não") == 1) {
						if (countitem(610) > 9) {
							mes "[Feylin]";
							mes "Eu entendo. Então vamos começar.";
							mes "Woombahsokasabahah!";
							mes "Woombah woombah hoombabah!";
							mes "Yeeeeep!";
							next;
							.@nif_revive1 = rand(1,100);
							if (.@nif_revive1 > 0) && (.@nif_revive1 < 88) {
								mes "[Feylin]";
								mes "...Me desculpe.";
								mes "Eu falhei ao invocar";
								mes "como você pode ver. Eu realmente peço";
								mes "sinceras desculpas.";
								emotion e_omg;
								delitem 748,1; //Rosa_Eterna
								delitem 610,10;//Folha de Yggdrasil
								nif_revive = 5;
								changequest 11041,11042;
								percentheal -99,0;
								next;
								mes "[Feylin]";
								mes "Infelizmente, parece ser";
								mes "impossível invocar o";
								mes "espírito da pessoa que";
								mes "você deseja falar...";
								close;
							}
							else if (.@nif_revive1 > 87) && (.@nif_revive1 < 101) {
								delitem 748,1; //Witherless_Rose
								delitem 610,10; //Leaf_Of_Yggdrasil
								percentheal -50,0;
								mes "[Feylin]";
								mes "..........";
								mes "..........";
								next;
								mes "[Elly]";
								mes "..........";
								mes ".....Humm...";
								mes "...Não estava apenas morto?";
								mes "Hum, não é?";
								next;
								select("Eu trouxe uma mensagem de seu marido.");
								mes "[Elly]";
								mes "Ah...Eu entendo...Eu sinto muito por não ter conseguido ajudá-lo antes de ter morrido..";
								mes "Então, qual é sua mensagem para mim?";
								next;
								select("Perdoe-o pelo que aconteceu há um mês.");
								mes "[Elly]";
								mes "*Suspiro* Ele é um marido tão bom...";
								mes "Ele presta tanta atenção, até mesmo para";
								mes "pequenas coisas. Embora, eu";
								mes "deva ter sido a única";
								mes "a perdoá-lo...";
								next;
								mes "[Elly]";
								mes "Por favor lhe entregue esta caixa, e diga";
								mes "para ele se esquecer de mim. Ele deve";
								mes "viver o resto de sua vida feliz";
								mes "com outra pessoa. Ah, também diga a";
								mes "ele que eu o perdôo.";
								nif_revive = 6;
								changequest 11040,11043;
								getitem 934,1; //Mementos
								next;
								mes "[Elly]";
								mes "Estou feliz de ouvir sobre";
								mes "meu marido. Mas eu";
								mes "acho que eu tenho que ir agora.";
								mes "Obrigado por sua mensagem.";
								mes "Adeus...";
								next;
								mes "[Elly]";
								mes ".....................";
								mes "....................";
								next;
								mes "[Feylin]";
								mes "Eu acho que você conseguiu o que";
								mes "você queria. Bem, então, tome cuidado";
								mes "quando você voltar...";
								close;
							}
						}
						else {
							mes "[Feylin]";
							mes "Se você não tem os";
							mes "itens, nós não podemos continuar.";
							mes "Lembre-se, eu preciso de 10 Folhas";
							mes "de Yggdrasil para recriar essa magia.";
							mes "Por favor traga-as e";
							mes "será possível.";
							close;
						}
					}
					mes "[Feylin]";
					mes "Eu espero que você me traga os itens";
					mes "primeiro. Mas... Eu não posso";
					mes "prometer que";
					mes "essa magia será conjurada com sucesso.";
					close;
				}
				mes "[Feylin]";
				mes "Hum... Você não parece ter";
				mes "a força e o vigor necessário para";
				mes "conjurar essa magia. Com";
				mes "sua força atual, essa";
				mes "magia irá matá-lo.";
				mes "Eu não posso correr esse risco.";
				close;
			}
			mes "[Feylin]";
			mes "Você fez uma boa escolha.";
			mes "Não é uma boa idéia executar";
			mes "esse tipo de magia no";
			mes "primeiro lugar que imaginar por causa";
			mes "dos riscos envolvidos...";
			close;
		}
		mes "[Feylin]";
		mes "Não importa quantas vezes você me perguntar,";
		mes "eu não vou te ajudar. Por favor";
		mes "saia deste lugar assim que puder.";
		close;
	}
	mes "[Feylin]";
	mes "O que você fez para chegar nessa";
	mes "cidade tão perigosa? Por favor saia";
	mes "deste lugar assim que puder.";
	close;
}
