//    ________________________________________________________
//   /                                                        \
//   |       _           _   _   _                            |
//   |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
//   |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
//   |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
//   |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
//   |                                                        |
//   |                    brAthena Script                     |
//   |--------------------------------------------------------|
//   | Nome do Script: Quests Aldebaran                       |
//   |--------------------------------------------------------|
//   | Criado por: eA/rA/brA Dev. Team                        |
//   |--------------------------------------------------------|
//   | Versões eA/rA/brA: 1.3/1.3/1.1                         |
//   |--------------------------------------------------------|
//   | Descrição: Quests dos itens para cabeça 'Espelho de    |
//   | Médico', 'Chapéu com Pluma', 'Máscara do Fantasma'     |
//   | e 'Chapéu Chinês'.                                     |
//   |--------------------------------------------------------|
//   | Changelog:                                             |
//   | 1.0 Cabeçalho Criado [Holy]                            |
//   | 1.1 Tradução PT-BR [Barsaglini]                        |
//   |--------------------------------------------------------|
//   | - Anotações                                            |
//   \________________________________________________________/

aldeba_in,152,166,4	script	Mercador#01	4_M_04,{
	mes "[Mercador]";
	mes "Quem é este homem misterioso?";
	mes "Eu, o enigmático e charmoso 'Mercador?'";
	next;
	mes "[Mercador]";
	mes "Viajando pelo continente de Rune-Midgard, com todos os seus chapéus maravilhosos? Ninguém sabe...";
	next;
	mes "[Mercador]";
	mes "Para que propósito posso colecionar e trocar esses chapéus de todo o mundo? Escolha um chapéu, vão desvendando o mistério...";
	next;
	mes " (1) ^3355FFEspelho de Médico^000000:";
	mes "1 Faixa Marrom + 50 Ferros + 1 Diamante Rachado + 3.500 zenys";
	mes " (2)^3355FFChapéu com Pluma^000000:";
	mes "1 Chapéu Romântico + 300 Plumas de Aves + 500 zenys";
	mes " (3) ^3355FFMáscara do Fantasma^000000:";
	mes "20 Ferro + 1 Planta da Melodia + 5.000 zenys";
	mes " (4) ^3355FFChapéu Chinês^000000:";
	mes "120 Troncos + 10.000 zenys ";
	next;
	switch(select("Espelho de Médico.:Chapéu com Pluma.:Máscara do Fantasma.:Chapéu Chinês.")) {
	case 1:
		if ((countitem(2275) > 0) && (countitem(998) > 49) && (countitem(733) > 0) && (Zeny > 3499)) {
			delitem 2275,1;  // Faixa Marrom
			delitem 998,50;  // Ferro
			delitem 733,1;   // Diamante Rachado
			set Zeny,Zeny-3500;
			mes "[Mercador]";
			mes "Hm! Você não tem uma licença médica, não é? Está tudo bem, eu ouvi sobre um Gatuno, o médico não licenciado que realizou milagres médicos! Mas... Isso pode ter sido uma história em quadrinhos.";
			next;
			mes "[Mercador]";
			mes "Que seja. Só não seja pego com isso.";
			getitem 2273,1;  // Espelho de Médico
			close;
		}
		else {
			mes "[Mercador]";
		  mes "Você cara!~";
		  mes "Verifique os requisitos de novo.";
		  mes "Você não parece idiota. Então... caia na real.";
			close;
		}
	case 2:
		if ((countitem(2247) > 0) && (countitem(916) > 299) && (Zeny > 499)) {
			delitem 2247,1;   // Chapéu Romântico
			delitem 916,300;  // Plumas
			Zeny -= 500;
			mes "[Mercador]";
			mes "Ah! Você tem bom senso de moda. Eu sei que você teve um tempo difícil coletando esse material, mas este chapéu vale a pena. Pegue. Tudo que você precisa agora é de um casaco de pele e uma bengala!";
			getitem 5018,1; // Chapéu com Pluma
			close;
		}
		else {
			mes "[Mercador]";
			mes "Ei cara~";
			mes "Verifique os requisitos de novo. Você não parece idiota. Então... caia na real.";
			close;
		}
	case 3:
		if ((countitem(998) > 19) && (countitem(707) > 0) && (Zeny > 4999)){
			delitem 998,20;  // Ferro
			delitem 707,1;   // Planta da Melodia
			Zeny -= 5000;
			mes "[Mercador]";
			mes "Isso aqui? É um pouco assustador, embora ele tenha algum tipo de aspecto romântico. O que você acha? Você gosta disso? Tudo bem, pegue, é seu!";
			getitem 2281,1; // Máscara do Fantasma
			close;
		}
		else {
			mes "[Mercador]";
		  mes "Palhaço. Verifique os requisitos de novo. Não é fácil conseguir uma ^3355FFMáscara do Fantasma^000000. Então... caia na real.";
			close;
		}
	case 4:
		if ((countitem(1019) > 119) && (Zeny > 9999)){
			delitem 1019,120; // Tronco
			Zeny -= 10000;
			mes "[Mercador]";
		mes "Se você tiver a oportunidade de visitar a Vila Uplander, em Payon, visite e conheça o artesão do Chapéu Chinês.";
			next;
			mes "[Mercador]";
			mes "O Chapéu Chinês nunca foi vendido para comerciantes além de mim, já que apenas eu posso reconhecer a sua qualidade. Devido à sua raridade, o Chapéu Chinês se tornou um produto muito especial e excepcional. Tudo bem! Pegue, é seu!";
			getitem 2280,1;  // Chapéu Chinês
			close;
		}
		else {
			mes "[Mercador]";
		mes "*Aiai*~ Verifique meus requisitos de novo. Então... Você não pareçe um idiota, caia na real.~";
			close;
		}
	}
}
