//===== Hercules Script ======================================
//= Renters
//===== By: ==================================================
//= kobra_k88, mod by Lupus
//===== Current Version: =====================================
//= 2.2
//===== Description: =========================================
//= Knight and Crusader Peco Peco Breeders, Falcon Breeder scripts
//===== Additional Comments: =================================
//= Fully working
//= Added another Falcon Master into the Hunters Guild [Lupus]
//= 1.2: replaced checkoption(x) with checkriding,checkfalcon [Lupus]
//= 1.3: Added support Adv Classes + Baby Class [Lupus]
//= 1.4: Added different prices for normal, advanced and baby classes
//= but you could set them to the same [Lupus]
//= 1.5: Fixed spelling mistakes [Nexon]
//= 1.5a: Small fix (Falcon Taming -> Falcon Mastery) [Komurka]
//= 1.6: Moved the Falcon Master to Hugel [Poki#3]
//= 1.7 Updated to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.8 Added 3rd Job creature NPCs (Dragon/Gryphon) [L0ne_W0lf]
//= 1.9 Enabled Gryphon Renter NPC and added Mado Gear NPC. [Masao]
//= 2.0 Fixed problems with third classes and new mounts. [Euphy]
//= 2.1 Moved some renters to a separate renewal file. [Daegaladh]
//= 2.2 Added warg checks. [Euphy]
//============================================================

// Criador de Peco-Pecos (para Cavaleiros)------------------------------------
prontera,55,350,5	script	Criador de Peco-Pecos#knt	8W_SOLDIER,{
	if(Upper==0) .@price = 2500; //Peco Normal - preço padrão
	if(Upper==1) .@price = 2500; //Peco Blindado
	if(Upper==2) .@price = 2500; //Peco Bebê

	mes "[Criador de Peco-Pecos]";
	if (BaseJob == Job_Knight && Class < Job_Rune_Knight) {
		mes "Seja bem vindo.";
		mes "Honrado Cavaleiro,";
		mes "Você gostaria de alugar";
		mes "um Peco Peco? A taxa";
		mes "do aluguel é de "+.@price+" zenys.";
		next;
		switch(select("Alugar Peco Peco:Cancelar")) {
		case 1:
			if (Zeny < .@price) {
				mes "[Criador de Peco-Pecos]";
				mes "Você não";
				mes "possui zenys suficientes.";
				mes "Você está...";
				mes "quebrado por acaso?";
				close;
			}
			else if(getskilllv(KN_RIDING) == 0) {
				mes "[Criador de Peco-Pecos]";
				mes "Sinto muito, mas você não";
				mes "pode utilizar esse serviço";
				mes "no momento. Por favor, aprenda";
				mes "a habilidade Montaria primeiro.";
				close;
			}
			else if(checkmount()) {
				mes "[Criador de Peco-Pecos]";
				mes "Você já está";
				mes "montado em um";
				mes "Peco Peco.";
				close;
			} else if(hascashmount()) {
				mes "[Criador de Peco-Pecos]";
				mes "Por favor, remova a sua montaria atual primeiro.";
				close;
			}
			Zeny -= .@price;
			setmount(MOUNT_PECO);
			close;
		case 2:
			mes "[Criador de Peco-Pecos]";
			mes "Entendo.";
			mes "Bem então,";
			mes "tenha um bom dia.";
			close;
		}
	} else {
		mes "Sinto muito, mas esses";
		mes "Peco Pecos estão somente";
		mes "Disponíveis para cavaleiros";
		mes "e Lordes.";
		close;
	}
}

// Grande Criador de Peco-Pecos (para Templários)------------------------------
prontera,232,318,3	script	Criador de Peco-Pecos#cru	8W_SOLDIER,{
	if(Upper==0) .@price = 3500; //Peco Normal - preço padrão
	if(Upper==1) .@price = 3500; //Peco Blindado
	if(Upper==2) .@price = 3500; //Peco Bebê

	mes "[Criador de Peco-Pecos]";
	if (BaseJob == Job_Crusader && Class < Job_Rune_Knight) {
		if(Upper != 1 ) mes "Bem vindo, Templário.";
		else mes "Bem vindo, Paladino.";
		mes "Eu tenho um";
		mes "Peco Peco especial preparado";
		mes "para você. Alugue um aqui";
		mes "por "+.@price+" zenys.";
		next;
		switch(select("Alugar Peco Peco:Sair")) {
		case 1:
			if (Zeny < .@price) {
				mes "[Criador de Peco-Pecos]";
				mes "Você não";
				mes "possui zenys suficientes.";
				mes "Caso queira alugar um";
				mes "Peco Peco por favor";
				mes "tenha em mãos "+.@price+" zenys...";
				close;
			}
			else if(getskilllv(KN_RIDING) == 0) {
				mes "[Criador de Peco-Pecos]";
				mes "Primeiro você precisa";
				mes "aprender a habilidade Montaria";
				mes "caso você queira alugar um de meus pecos.";
				close;
			}
			else if(checkmount()) {
				mes "[Criador de Peco-Pecos]";
				mes "Você já está";
				mes "montado em um Peco Peco.";
				close;
			} else if(hascashmount()) {
				mes "[Criador de Peco-Pecos]";
				mes "Por favor, remova a sua montaria atual antes de alugar outra.";
				close;
			}
			Zeny -= .@price;
			setmount(MOUNT_PECO);
			close;
		case 2:
			mes "[Criador de Peco-Pecos]";
			mes "Te vejo por ai.";
			close;
		}
	} else {
		mes "O que posso fazer por você?";
		mes "Tenha em mente que";
		mes "Este serviço de aluguel";
		mes "de peco pecos é restrito a";
		mes "Templários e Paladinos.";
		close;
	}
}

// Criador de Falcões--------------------------------------------------------------
hu_in01,381,304,5	script	Criador de Falcões#hnt	8W_SOLDIER,{
	if(Upper==0) .@price = 2500; //Falcão Normal - preço padrão
	if(Upper==1) .@price = 2500; //Falcão de Cachicol
	if(Upper==2) .@price = 2500; //Falcão Bebê

	mes "[Criador de Falcões]";
	if (BaseJob == Job_Hunter) {
		if (checkoption(Option_Wug) || checkoption(Option_Wugrider)) {
			mes "Hm...";
			mes "Você não pode alugar um falcão";
			mes "até que você remova primeiro o seu Lobo!";
			close;
		}
		mes "Você precisa de um Falcão?";
		mes "Eu posso te alugar o seu próprio";
		mes "confiável Pássaro de Rapina";
		mes "por apenas "+.@price+" zenys~";
		next;
		switch(select("Alugar Falcão:Cancelar")) {
		case 1:
			if (checkoption(Option_Wug) || checkoption(Option_Wugrider)) {
				mes "[Criador de Falcões]";
				mes "Hm...";
				mes "Você não pode alugar um falcão";
				mes "até que você remova primeiro o seu Lobo!";
				close;
			}
			if (Zeny < .@price) {
				mes "[Criador de Falcões]";
				mes "O que é isso?";
				mes "Você não tem";
				mes "zenys suficientes?!";
				mes "É melhor você começar";
				mes "a caçar dinheiro";
				mes "em vez de monstros~";
				close;
			}
			else if(getskilllv("HT_FALCON") == 0) {
				mes "[Criador de Falcões]";
				mes "Poxa~";
				mes "Eu não posso te alugar";
				mes "um falcão no momento pois";
				mes "você não possui a Habilidade";
				mes "Adestrar Falcão aprendida";
				mes "no momento. Lide com isso.";
				close;
			}
			else if(checkfalcon()) {
				mes "[Criador de Falcões]";
				mes "Hm...";
				mes "Você já possui";
				mes "um Falcão. Tem certeza";
				mes "de que não está ficando cego?";
				close;
			}
			Zeny -= .@price;
			setfalcon;
			close;
		case 2:
			mes "[Criador de Falcões]";
			mes "Es-Espero, onde pensa";
			mes "que você vai? Estes";
			mes "Falcões são do mais alto nível,";
			mes "Eu garanto! Vamos lá alugue um~";
			close;
		}
	} else {
		mes "Jovem tolo!";
		mes "Falcões só podem";
		mes "serem usados por Caçadores";
		mes "e Atiradores de Elite, capish?";
		mes "...Heh heh, está com inveja não é?";
		close;
	}
}